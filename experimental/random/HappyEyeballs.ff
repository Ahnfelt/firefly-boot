// https://datatracker.ietf.org/doc/html/rfc8305#section-5

data Ip(ipAddress: String, seconds: Duration, succeed: Bool)

main(system: NodeSystem): Unit {
    let ips = [
        Ip("1.1.1.1", Duration(1.7), False)
        Ip("2.2.2.2", Duration(0.4), False)
        Ip("3.3.3.3", Duration(1.5), True)
        Ip("4.4.4.4", Duration(0.6), True)
    ]
    let channel = system.tasks().channel(capacity = 2)
    channel.write(Unit)
    channel.write(Unit)
    let winner = happyEyeballs(system.tasks(), channel, ips)
    Log.debug(winner)
}

happyEyeballs(taskSystem: TaskSystem, channel: Channel[Unit], ips: List[Ip]): String {
    let ip = ips.grabFirst()
    let nextChannel = taskSystem.channel(capacity = 2)
    taskSystem.race([
        {
            channel.read()
            Channel.readOr(channel, {_}).timeout(Duration(0.5), {})
            nextChannel.write(Unit)
            try {
                taskSystem.sleep(ip.seconds)
                if(!ip.succeed) { panic("Network error") }
                ip.ipAddress
            } catchAny { error =>
                nextChannel.write(Unit)
                error.rethrow()
            } grab()
        }
        {
            happyEyeballs(taskSystem, nextChannel, ips.dropFirst())
        }
    ])
}
