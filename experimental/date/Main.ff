dependency ff:webserver:0.0.0
import WebServer from ff:webserver
import Date

browserMain(system: BrowserSystem) {
    let now = Date.new("Europe/Copenhagen", 2025, 2, 1)
    let firstTuesday = now.toStream {_.add(days = 1)}.find {_.dayOfWeek() == Date.tuesday}
    let tuesdays = firstTuesday.grab().toStream {_.add(weeks = 1)}.map {_.startOfDay()}
    let meetups = tuesdays.filter {d => d.add(weeks = 1).month() > d.month()}
    meetups.takeFirst(12).each {meetup =>
        Log.debug("Meetup: " + meetup.toRfc9557())
    }
}

buildMain(system: BuildSystem) {
    let jsAssets = system.compileForBrowser("Main.ff").assets()
    Log.debug(jsAssets.list("/").join(" "))
    let assets = AssetSystem.create().addAssets("/js", jsAssets)
    Log.debug(assets.files.keys().toList().join(" "))
    system.setAssets(assets)
}

nodeMain(system: NodeSystem) {
    let host = system.arguments().grab(0)
    let port = system.arguments().grab(1).grabInt()
    WebServer.new(system, host, port).listen {request =>
        let path = request.readPath()
        let segments = path.split('/').filter {s => s != "" && s != "." && s != ".."}
        segments.{
            | ["js", ...] => 
                request.writeHeader("Content-Type", "text/javascript; charset=UTF-8")
                request.writeStream(system.assets().readStream(path))
            | [] =>
                request.writeHeader("Content-Type", "text/html; charset=UTF-8")
                request.writeText("<!doctype html>")
                request.writeText("<html lang='en' style='background-color: #ffffff; color: #333333; width: 100%; height: 100%; color-scheme: light;'>")
                request.writeText("<head>")
                request.writeText("<title>Date</title>")
                request.writeText("<meta name='viewport' content='width=device-width, initial-scale=1.0'>")
                request.writeText("<meta name='theme-color' content='#ecc45e'>")
                request.writeText("<script src='https://cdn.jsdelivr.net/npm/temporal-polyfill@0.2.5/global.min.js'></script>")
                request.writeText("<script type='module' src='" + "/js/script/script/Main.mjs" + "'></script>")
                request.writeText("</head>")
                request.writeText("<body style='margin: 0; padding: 0; width: 100%; height: 100%; touch-action: manipulation;'>")
                request.writeText("</body>")
                request.writeText("</html>")
            | _ =>
                request.writeStatus("404 Not Found")
        }
    }
}
