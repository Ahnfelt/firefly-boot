data Expression {
    EVariable(name: String)
    EApply(function: Expression, argument: Expression)
    ELambda(variable: String, body: Expression)
    EAnnotate(type: Type, expression: Expression)
}

data Type(name: String, typeArguments: List[Type])

data Context(variables: Map[String, Type])

data TypeError(message: String)


check(c0: Context, t0: Type, e0: Expression): Unit {
    e0.{
        | EVariable _ => 
            let t1 = infer(c0, e0)
            checkSame(t0, t1)
        | EApply _ => 
            let t1 = infer(c0, e0)
            checkSame(t0, t1)
        | ELambda(x1, e1) => 
            t0.{
                | Type("Function", [t2, t3]) =>
                    let c2 = c0.Context(variables = c0.variables.add(x1, t2))
                    check(c2, t3, e1)
                    t3
                | _ =>
                    throw(TypeError("Can't call non-functions"))
            }
        | EAnnotate(t1, e1) =>
            check(c0, t1, e1)
    }
}


infer(c0: Context, e0: Expression): Type {
    e0.{
        | EVariable(x1) => 
            c0.variables.get(x1).{
                | None => 
                    throw(TypeError("Variable not in scope: " + x1))
                | Some(t1) => 
                    t1
            }
        | EApply(e1, e2) => 
            let t1 = infer(c0, e1)
            t1.{
                | Type("Function", [t2, t3]) =>
                    check(c0, t2, e2)
                    t3
                | _ =>
                    throw(TypeError("Can't call non-functions"))
            }
        | ELambda _ => 
            throw(TypeError("Lambda functions must be explicitly typed"))
        | EAnnotate(t1, e1) =>
            check(c0, t1, e1)
            t1
    }
}


checkSame(t1: Type, t2: Type): Unit {
    if(t1.name != t2.name) {throw(TypeError("Type mismatch: " + t1.name + " vs. " + t2.name))}
    if(t1.typeArguments.size() != t2.typeArguments.size()) {
        throw(TypeError(
            "Type mismatch: " + t1.name + " with " + 
            t1.typeArguments.size() + " vs. " + t2.typeArguments.size() +
            " type arguments."
        ))
    }
    t1.typeArguments.zip(t2.typeArguments).each {| Pair(t3, t4) =>
        checkSame(t3, t4)
    }
}
