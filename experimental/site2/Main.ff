import WebServer from ff:webserver
import WebRoute from ff:webserver
import Html
import Router

nodeMain(system: NodeSystem): Unit {
    let host = system.arguments().grab(0)
    let port = system.arguments().grab(1).grabInt()
    let context = RouteContext(system)
    let handlers = WebRouteHandler(Array.new())
    Router.handlers(handlers)
    system.writeLine("Listening on " + host + ":" + port)
    WebServer.new(system, host, port).listen {request =>
        let path = request.readPath()
        let segments = path.split('/').filter {s => s != "" && s != "." && s != ".."}
        segments.{
            | ["js", ...] => 
                let asset = segments.map {"/" + _}.join()
                request.writeHeader("Content-Type", "text/javascript; charset=UTF-8")
                request.writeStream(system.assets().readStream(asset))
            | _ =>
                if(!handlers.handle(request, context)) {
                    request.writeStatus("404 Not Found")
                }
                Unit
        }
    }    
}

buildMain(system: BuildSystem) {
    mutable assets = AssetSystem.create()
    Router.modulesWithBrowserMain.each {moduleName =>
        let browser = system.compileForBrowser(moduleName + ".ff") // .bundle()
        assets = assets.addAssets("/js/", browser.assets())
    }
    system.setAssets(assets)
}
