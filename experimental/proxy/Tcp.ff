import Tcp

capability TcpServer(
    system: NodeSystem
    jsValue: JsValue
    host: Option[String]
    port: Option[Int]
    path: Option[String]
    backlog: Int
    exclusive: Bool
    ipv6Only: Bool
    reusePort: Bool
    readableAll: Bool
    writeableAll: Bool
)

capability TcpSocket(system: NodeSystem, jsValue: JsValue)

new(
    system: NodeSystem
    port: Int // TODO
): TcpSocket {
    let net = Js.import("node:net")
    let jsValue = net->createConnection(Js->(
        port = port
    ))
    TcpSocket(system, jsValue)    
}

newServer(
    system: NodeSystem
    host: Option[String] = None
    port: Option[Int] = None
    path: Option[String] = None
    exclusive: Bool = False
    allowHalfOpen: Bool = False 
    highWaterMark: Int = 65536
    keepAlive: Bool = False
    noDelay: Bool = False
    backlog: Int = 511
    ipv6Only: Bool = False
    reusePort: Bool = False
    readableAll: Bool = False
    writeableAll: Bool = False
): TcpServer {
    let net = Js.import("node:net")
    let jsValue = net->createServer(Js->(
        allowHalfOpen = allowHalfOpen
        highWaterMark = highWaterMark
        keepAlive = keepAlive
        noDelay = noDelay
    ))
    TcpServer(system, jsValue, host, port, path, backlog, exclusive, ipv6Only, reusePort, readableAll, writeableAll)
}

extend self: TcpServer {
    
    listen(body: TcpSocket => Unit) {
        Js.withSignal {signal =>
            self.jsValue->listen(Js->(
                host = Js.orUndefined(self.host)
                port = Js.orUndefined(self.port)
                path = Js.orUndefined(self.path)
                backlog = self.backlog
                exclusive = self.exclusive
                ipv6Only = self.ipv6Only
                reusePort = self.reusePort
                readableAll = self.readableAll
                writeableAll = self.writeableAll
                signal = signal
            ))
            self.jsValue->on("connection", Js->{jsSocket =>
                try {
                    body(TcpSocket(self.system, jsSocket)) // What to do if this throws?
                } finally {
                    jsSocket->destroySoon()
                }
            })
            Js.awaitCancellablePromise {resolve, reject, cleanup => 
                self.jsValue->on("close", Js->{
                    resolve(Unit)
                })
                self.jsValue->on("error", Js->{jsError => 
                    reject(jsError?)
                })
            }
        }
    }
    
    close() {
        Js.await(self.jsValue->close())
    }
    
}

extend self: TcpSocket {
    
    readStream(): Stream[Buffer] {
        Path.internalReadStream({self.jsValue}, close = False)
    }
    
    writeStream(stream: Stream[Buffer]) {
        stream.each {self.writeBuffer(_)}
    }

    writeBuffer(buffer: Buffer): Unit {
        if(!self.jsValue->write(Js->Uint8Array->(buffer!->buffer, buffer!->byteOffset, buffer!->byteLength))?) {
            Js.withSignal {signal =>
                Js.await(Js->Promise->(Js->{resolve, reject =>
                    let jsDoReject = Js->{_ => reject->callValue1(signal->reason?)}
                    signal->addEventListener("abort", jsDoReject)
                    self.jsValue->once("drain", Js->{
                        signal->removeEventListener("abort", jsDoReject)
                        resolve.callValue0()
                    })
                }))
            }
        }
    }
    
    writeText(text: String): Unit {
        self.writeBuffer(text.toBuffer())
    }

    writeLine(text: String): Unit {
        self.writeText(text + "\n")
    }

}

nodeMain(system: NodeSystem) {
    let targetSocket = Tcp.new(system, 8080)
    Tcp.newServer(system, port = Some(8081)).listen {socket =>
        system.writeLine("Connected!")
        system.mainTask().all([
            {targetSocket.writeStream(socket.readStream())}
            {socket.writeStream(targetSocket.readStream())}
        ])
    }
}


