import Lux2 as Lux
import LuxEvent

capability Lux(
    document: LuxDocument
    hooks: Stack[LuxHook]
    jsSystem: JsSystem
    mutable render: Option[() => Unit]
    mutable element: LuxElement
    mutable keys: Option[StringMap[JsValue]]
    mutable key: String
    mutable properties: Option[StringMap[JsValue]]
)

capability LuxHook(name: String, tag: AnyTag, location: SourceLocation)

class LuxElement(element: JsValue, mutable child: Int)

class LuxDocument(document: JsValue)

extend self: LuxElement {
    childAt(index: Int): JsValue {
        self.element.get("childNodes").get(index)
    }
    insertBefore(newNode: JsValue, referenceNode: JsValue): JsValue {
        self.element.call2("insertBefore", newNode, referenceNode)
    }
    removeAt(index: Int): Bool {
        let node = self.childAt(index)
        let remove = !node.isNullOrUndefined()
        if(remove) {
            self.element.call1("removeChild", node)
        }
        remove
    }
}

extend self: LuxDocument {
    createTextNode(value: String): JsValue {
        self.document.call1("createTextNode", value)
    }

    createElement(tagName: String): JsValue {
        self.document.call1("createElement", tagName)
    }
}

extend self: Lux {
    
    text(value: String) {
        let oldNode = self.element.childAt(self.element.child)
        let oldValue = if(!oldNode.isNullOrUndefined()) {oldNode.get("data")} else {oldNode}
        if(oldValue.isNullOrUndefined() || oldValue.grabString() != value) {
            let node = self.document.createTextNode(value)
            self.element.insertBefore(node, oldNode)
        }
        self.element.child += 1
    }
    
    new(tagName: String, body: () => Unit = {}) {
        let newKey = if(self.key != "") {tagName.upper() + ">" + self.key} else {""}
        let oldNode = self.element.childAt(self.element.child)
        let oldKey = if(!oldNode.isNullOrUndefined()) {oldNode.get("luxKey")} else {oldNode}
        let match = if(newKey != "") {
            !oldKey.isNullOrUndefined() && 
            oldKey.grabString() == newKey
        } else {
            oldKey.isNullOrUndefined() && 
            !oldNode.isNullOrUndefined() &&
            !oldNode.get("tagName").isNullOrUndefined() && 
            oldNode.get("tagName").grabString() == tagName.upper()
        }
        let node = if(match) {
            oldNode
        } else {
            let newNode = if(newKey == "") {self.document.createElement(tagName)} else {
                let keys = self.keys.else {
                    let map = StringMap.make()
                    mutable i = self.element.child
                    mutable c = self.element.childAt(i)
                    while {!c.isNullOrUndefined()} {
                        let k = c.get("luxKey")
                        if(!k.isNullOrUndefined()) {map.set(k.grabString(), c)}
                        c = self.element.childAt(i)
                        i += 1
                    }
                    self.keys = Some(map)
                    map
                }
                if(keys.has(newKey)) {
                    let foundNode = keys.grab(newKey)
                    keys.remove(newKey)
                    foundNode
                } else {
                    let createdNode = self.document.createElement(tagName)
                    createdNode.set("luxKey", newKey)
                    createdNode
                }
            }
            self.element.insertBefore(newNode, oldNode)
            newNode
        }
        let savedProperties = self.properties
        let savedKeys = self.keys
        let savedElement = self.element
        self.properties = None
        self.element = LuxElement(node, 0)
        self.key = ""
        try {
            body()
        } finally {
            doWhile {self.element.removeAt(self.element.child)}
            patchProperties(self)
            self.properties = savedProperties
            self.keys = savedKeys
            self.element = savedElement
            self.element.child += 1
        } grab()
    }

    div(body: () => Unit = {}) {self.new("div", body)}
    span(body: () => Unit = {}) {self.new("span", body)}
    label(body: () => Unit = {}) {self.new("label", body)}
    button(body: () => Unit = {}) {self.new("button", body)}
    form(body: () => Unit = {}) {self.new("form", body)}
    input(body: () => Unit = {}) {self.new("input", body)}

    keyed(key: String, body: () => Unit) {
        try {
            self.key = key
            body()
        } finally {
            self.key = ""
        } grab()
    }
    
    keyedList[T](values: List[T], getKey: T => String, body: T => Unit) {
        try {
            values.each {item =>
                self.key = getKey(item)
                body(item)
            }
        } finally {
            self.key = ""
        } grab()
    }

    with[V: IsJsValue](property: String, value: V) {
        let properties = self.properties.else {
            let map = StringMap.make()
            self.properties = Some(map)
            map
        }
        properties.set(property, self.jsSystem.value(value))
    }
    
    withId(value: String) {self.with("id", value)}
    withClassName(value: String) {self.with("className", value)}
    withStyle(value: String) {self.with("style", value)}
    
    on(event: String, handler: LuxEvent => Unit) {
        
    }

    onClick(handler: LuxEvent => Unit) {self.on("click", handler)}
    onInput(handler: LuxEvent => Unit) {self.on("input", handler)}
        
    useState[T: HasAnyTag](initialValue: T, body: (T, T => Unit) => Unit, location: SourceLocation = SourceLocation.callSite()) {
        //let hook = UseState(initialValue, body, location)
        //self.hooks.push(Any.toAny(hook))
    }

    useMemo1[A1, T](a1: A1, compute: A1 => T, body: T => Unit = {_ => }) {
        
    }
    
    useSuspense(suspense: () => Unit, body: () => Unit) {
        
    }

}

patchProperties(self: Lux) {
    let oldProperties = self.element.element.get("luxProperties")
    self.properties.{
        | None => 
            if(!oldProperties.isNullOrUndefined()) {
                oldProperties.grabStringMap().each {key, _ =>
                    self.element.element.set(key, self.jsSystem.undefined())
                }
                self.element.element.set("luxProperties", self.jsSystem.undefined())
            }
            Unit
        | Some(map) => 
            if(!oldProperties.isNullOrUndefined()) {
                let oldMap = oldProperties.grabStringMap()
                oldMap.each {key, value =>
                    if(!map.has(key)) {
                        self.element.element.set(key, self.jsSystem.undefined())
                    }
                }
                map.each {key, value =>
                    if(!oldMap.has(key)) {
                        self.element.element.set(key, value)
                    }
                }
            } else {
                map.each {key, value =>
                    self.element.element.set(key, value)
                }
            }
            self.element.element.set("luxProperties", map)
    }    
}

render(jsSystem: JsSystem, element: JsValue, body: Lux => Unit) {
    mutable document = element
    while {!document.get("parentNode").isNullOrUndefined()} {
        document = document.get("parentNode")
    }
    let lux = Lux(
        jsSystem = jsSystem
        document = LuxDocument(document)
        hooks = Stack.make()
        render = None
        element = LuxElement(element, 0)
        keys = None
        key = ""
        properties = None
    )
    body(lux)
}

renderById(browserSystem: BrowserSystem, id: String, body: Lux => Unit) {
    let element = browserSystem.js().global().get("document").call1("getElementById", id)
    render(browserSystem.js(), element, body)
}
