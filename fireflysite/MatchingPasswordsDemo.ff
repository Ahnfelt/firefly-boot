import Guide
import Lux from ff:lux
import LuxEvent from ff:lux

name = "Matching passwords"

new(): Demo {
    Demo(
        name
        {render(_)}
    )
}

render(lux: Lux): Unit {
    function passwordInput(password: String, setPassword: String => Unit) {
        lux.div {
            lux.input {
                lux.set("type", "password")
                lux.set("autocomplete", "new-password")
                lux.setValue(password)
                lux.onInput {event =>
                    setPassword(event.text())
                }
            }
        }
    }
    lux.form {
        lux.useState(""): password1, setPassword1 => 
        lux.useState(""): password2, setPassword2 => 
        passwordInput(password1, setPassword1)
        passwordInput(password2, setPassword2)
        if(password2.size() != 0) {
            lux.text(
                if(password1 == password2) {
                    "Passwords match!"
                } else {
                    "Passwords don't match."
                }
            )
        }
    }
}

newDocument(): Document {
    Document([
        Section(name, [
            Paragraph([Text("Two passwords fields and a check that they match.")])
        ])
        Section("Demo", [
            LuxDemo(name)
        ])
        Section("Code", [
            CodeBlock("""
                render(lux: Lux): Unit {
                    function passwordInput(password: String, setPassword: String => Unit) {
                        lux.div {
                            lux.input {
                                lux.set("type", "password")
                                lux.set("autocomplete", "new-password")
                                lux.setValue(password)
                                lux.onInput {event =>
                                    setPassword(event.text())
                                }
                            }
                        }
                    }
                    lux.form {
                        lux.useState(""): password1, setPassword1 => 
                        lux.useState(""): password2, setPassword2 => 
                        passwordInput(password1, setPassword1)
                        passwordInput(password2, setPassword2)
                        if(password2.size() != 0) {
                            lux.text(
                                if(password1 == password2) {
                                    "Passwords match!"
                                } else {
                                    "Passwords don't match."
                                }
                            )
                        }
                    }
                }
            """, firefly = True)
        ])
    ])
}
