import WebServer from ff:webserver
import Lux from ff:lux
import Guide
import ReferenceAll
import ExamplesOverview
import PackagesOverview
import CommunityOverview
import FrontPage
import Styles

nodeMain(system: NodeSystem): Unit {
    let host = system.arguments().grab(0)
    let port = system.arguments().grab(1).grabInt()
    let cacheSalt = system.crypto().randomBuffer(16)
    WebServer.new(system, host, port).listen {request =>
        let path = request.readPath()
        if(path.startsWith("/reference/")) {
            serveGuideHtml("Firefly Reference", request)
        } elseIf {path.startsWith("/examples/")} {
            serveGuideHtml("Firefly Examples", request)
        } elseIf {path.startsWith("/packages/")} {
            serveGuideHtml("Firefly Packages", request)
        } elseIf {path.startsWith("/community/")} {
            serveGuideHtml("Firefly Community", request)
        } elseIf {path.startsWith("/front/")} {
            serveGuideHtml("Firefly", request)
        } elseIf {path.startsWith("/js/") && !path.contains("..")} {
            let asset = if(path == "/js/ff/fireflysite/Main.mjs" && system.assets().exists("/js/Main.bundle.js")) {
                "/js/Main.bundle.js"
            } else {
                path
            }
            serveAsset(system, cacheSalt, request, "text/javascript; charset=UTF-8", asset)
        } elseIf {path.startsWith("/favicon.ico")} {
            serveAsset(system, cacheSalt, request, "image/png", "/assets/firefly-logo-notext.png")
        } elseIf {path.startsWith("/assets/firefly-logo-yellow.png")} {
            serveAsset(system, cacheSalt, request, "image/png", "/assets/firefly-logo-yellow.png")
        } elseIf {path.startsWith("/assets/autocomplete-small.png")} {
            serveAsset(system, cacheSalt, request, "image/png", "/assets/autocomplete-small.png")
        } elseIf {path.startsWith("/assets/edit-time-error.png")} {
            serveAsset(system, cacheSalt, request, "image/png", "/assets/edit-time-error.png")
        } elseIf {path.startsWith("/assets/FireflyMono.ttf")} {
            serveAsset(system, cacheSalt, request, "font/ttf", "/assets/NotoSansMono-Regular.ttf")
        } elseIf {path.startsWith("/assets/FireflySans.ttf")} {
            serveAsset(system, cacheSalt, request, "font/ttf", "/assets/NunitoSans-VariableFont_YTLC,opsz,wdth,wght.ttf")
        } else {
            let parameters = if(request.readRawQueryString().size() == 0) {""} else {
                "?" + request.readRawQueryString()
            }
            request.writeHeader("Location", "https://www.firefly-lang.org" + path + parameters)
            request.writeStatus("302 Found")
        }
    }
}

browserMain(system: BrowserSystem): Unit {
    let demos = ExamplesOverview.demos()
    let guides = [
        Guide("/front/", [FrontPage.new()])
        Guide("/examples/", ExamplesOverview.mock())
        Guide("/reference/", ReferenceAll.mock())
        Guide("/packages/", [PackagesOverview.new()])
        Guide("/community/", [CommunityOverview.new()])
    ]
    guides.collect {guide => 
        if(system.urlPath().startsWith(guide.prefix)):
        Lux.renderById(system, "main") {lux => 
            let kebab = system.urlPath().dropFirst(guide.prefix.size())
            Guide.render(lux, system, guide.prefix, kebab, guides, demos)
        }
    }
}

buildMain(system: BuildSystem) {
    let browser = system.compileForBrowser("Main.ff")
    let assets = AssetSystem.create()
        .addAssets("/js/", browser.assets())
        .addAssets("/assets/", system.packageAssets().assets("/assets/"))
    system.setAssets(assets)
}

serveAsset(system: NodeSystem, salt: Buffer, request: WebRequest[WebResponse], contentType: String, asset: String): Unit {
    let etag = "\"" + system.crypto().hmacSha256(salt, asset.toBuffer()).toHex() + "\""
    request.writeHeader("Content-Type", contentType)
    if(request.readHeader("host") == Some("localhost:8080")) {
        request.writeHeader("Cache-Control", "public, max-age=0, must-revalidate")
    } else {
        request.writeHeader("Cache-Control", "public, max-age=60, must-revalidate")
    }
    request.writeHeader("ETag", etag)
    if(request.readHeader("if-none-match").any {_.trim() == etag}) {
        request.writeStatus("304 Not Modified")
    } else {
        request.writeStream(system.assets().readStream(asset))
    }
}

serveGuideHtml(title: String, request: WebRequest[WebResponse]): Unit {
    request.writeHeader("Content-Type", "text/html; charset=UTF-8")
    request.writeText("<!doctype html>")
    request.writeText("<html lang='en' style='background-color: #ffffff; color: #333333; width: 100%; height: 100%; color-scheme: light;'>")
    request.writeText("<head>")
    request.writeText("<title>" + title + "</title>")
    request.writeText("<meta name='viewport' content='width=device-width, initial-scale=1.0'>")
    request.writeText("<meta name='theme-color' content='#ecc45e'>")
    request.writeText("<link rel='preload' href='/assets/FireflyMono.ttf' as='font' crossorigin='anonymous'>")
    request.writeText("<link rel='preload' href='/assets/FireflySans.ttf' as='font' crossorigin='anonymous'>")
    request.writeText("<link rel='preload' href='/assets/firefly-logo-yellow.png' as='image'>")
    request.writeText("<style>@font-face { font-family: 'Firefly Mono'; font-display: fallback; src: url('/assets/FireflyMono.ttf'); unicode-range: U+000-5FF; }</style>")
    request.writeText("<style>@font-face { font-family: 'Firefly Sans'; font-display: fallback; src: url('/assets/FireflySans.ttf'); unicode-range: U+000-5FF; }</style>")
    request.writeText("</head>")
    request.writeText("<body style='margin: 0; padding: 0; width: 100%; height: 100%; touch-action: manipulation;'>")
    request.writeText("<div id='main'></div>")
    request.writeText("<script type='module' src='/js/ff/fireflysite/Main.mjs'></script>")
    request.writeText("</body>")
    request.writeText("</html>")
}
