import WebServer from ff:webserver
import Lux from ff:lux
import Guide
import GuideIntroduction
import ExamplesOverview
import Styles
import CountingButtonDemo

nodeMain(system: NodeSystem): Unit {
    let host = system.arguments().grab(0)
    let port = system.arguments().grab(1).grabInt()
    WebServer.new(system, host, port).listen {request =>
        let path = request.readPath()
        if(path.startsWith("/guide/")) {
            serveGuideHtml("Firefly Guide", request)
        } elseIf {path.startsWith("/examples/")} {
            serveGuideHtml("Firefly Examples", request)
        } elseIf {path.startsWith("/js/") && !path.contains("..")} {
            request.writeHeader("Content-Type", "text/javascript; charset=UTF-8")
            request.writeText(system.assets().readText(path))
        } else {
            let parameters = if(request.readRawQueryString().size() == 0) {""} else {
                "?" + request.readRawQueryString()
            }
            request.writeHeader("Location", "https://www.firefly-lang.org" + path + parameters)
            request.writeStatus("302 Found")
        }
    }
}

browserMain(system: BrowserSystem): Unit {
    if(system.urlPath().startsWith("/guide/")) {
        let kebab = system.urlPath().dropFirst("/guide/".size())
        Lux.renderById(system, "main") {lux => 
            Guide.render(lux, "/guide/", kebab, Guide(GuideIntroduction.mock()), [])
        }
    } else {
        let kebab = system.urlPath().dropFirst("/examples/".size())
        let demos = [CountingButtonDemo.new()]
        Lux.renderById(system, "main") {lux => 
            Guide.render(lux, "/examples/", kebab, Guide(ExamplesOverview.mock()), demos)
        }
    }
}

buildMain(system: BuildSystem) {
    let browser = system.compileForBrowser("Main.ff")
    let assets = AssetSystem.create()
        .addAssets("/js/", browser.assets())
    system.setAssets(assets)
}

serveGuideHtml(title: String, request: WebRequest[WebResponse]): Unit {
    request.writeHeader("Content-Type", "text/html; charset=UTF-8")
    request.writeText("<!doctype html>")
    request.writeText("<html lang='en' style='background-color: #303236; color: #dadada; width: 100%; height: 100%; color-scheme: dark;'>")
    request.writeText("<head>")
    request.writeText("<title>" + title + "</title>")
    request.writeText("<meta name='viewport' content='width=device-width, initial-scale=1.0'>")
    request.writeText("</head>")
    request.writeText("<body style='margin: 0; padding: 0; width: 100%; height: 100%'>")
    request.writeText("<div id='main'></div>")
    request.writeText("<script type='module' src='/js/script/script/Main.mjs'></script>")
    request.writeText("</body>")
    request.writeText("</html>")
}
