import Lux from ff:lux
import LuxEvent from ff:lux
import Css from ff:lux
import Tokenizer from ff:compiler
import Token from ff:compiler
import Styles
import DocumentParser


data Section(heading: String) {
    Section(
        blocks: List[Block]
    )
    SplitSection(
        first: Block
        second: Block
    )
}

data Block {
    Paragraph(inlines: List[Inline])
    Bullets(items: List[List[Inline]])
    CodeBlock(code: String, firefly: Bool = False)
    Image(url: String)
    Video(url: String)
    LuxDemo(demo: String)
}

data Inline {
    Text(text: String)
    Bold(text: String)
    Italic(text: String)
    Code(code: String, firefly: Bool = False)
    Link(text: String, url: String)
    Anchor(heading: String, title: Option[String] = None)
}

capability Demo(
    name: String
    render: Lux => Unit
)

renderTopbar(lux: Lux, menu: Bool, setMenu: Bool => Unit) {
    lux.add("div") {
        lux.cssClass(Styles.guideTopbarCss)
        lux.add("a") {
            lux.set("href", "/")
            lux.cssClass(Styles.guideTopbarLogoCss)
            lux.add("img") {
                lux.set("src", "/assets/image/firefly-logo-yellow.webp")
            }
            lux.div {
                lux.text("Firefly")
            }
        }
        lux.add("button") {
            lux.set("aria-label", "Toggle the menu")
            lux.cssClass(Styles.guideTopbarButtonCss)
            lux.onClick {event =>
                event.preventDefault()
                setMenu(True)
            }
        }
    }
}

renderSections(lux: Lux, sections: List[Section], demos: List[Demo]) {
    sections.pairs().each {| Pair(index, section) => 
        renderSection(lux, index == 0, section, demos)
    }
}

renderNext(lux: Lux, path: String, title: String) {
    lux.div {
        lux.cssClass(Styles.guideNextButtonCss)
        lux.add("a") {
            lux.cssClass(Styles.guideButtonCss)
            lux.set("href", path)
            lux.text("Next: " + title)
        }
    }
}

renderSection(lux: Lux, firstSection: Bool, section: Section, demos: List[Demo]) {
    section.{
        | Section(heading, blocks) => 
            lux.add(if(firstSection) {"h1"} else {"h2"}) {
                lux.cssClass(if(firstSection) {Styles.guideH1Css} else {Styles.guideH2Css})
                lux.text(heading)
            }
            blocks.each {renderBlock(lux, _, demos)}
        | SplitSection(heading, first, second) => 
            lux.add("div") {
                lux.cssClass(Styles.guideSplitCss)
                lux.add("div") {
                    lux.add("h2") {
                        lux.cssClass(Styles.guideSplitHeadingCss)
                        lux.text(section.heading)
                    }
                    renderBlock(lux, first, demos)
                }
                lux.add("div") {
                    renderBlock(lux, second, demos)
                }
            }
        }
}

renderBlock(lux: Lux, block: Block, demos: List[Demo]) {
    block.{
        | Paragraph(inlines) => renderParagraph(lux, inlines)
        | CodeBlock(code, firefly) => renderCodeBlock(lux, code, firefly)
        | Bullets(items) => renderBullets(lux, items)
        | Image(url) => renderImage(lux, url)
        | Video(url) => renderVideo(lux, url)
        | LuxDemo(name) => renderLuxDemo(lux, name, demos)
    }
}

renderParagraph(lux: Lux, inlines: List[Inline]) {
    lux.add("p") {
        renderInlines(lux, inlines)
    }
}

renderCodeBlock(lux: Lux, code: String, firefly: Bool) {
    let lines = code.lines().dropWhile {_.all {_ == ' '}}.reverse().dropWhile {_.all {_ == ' '}}.reverse()
    let indentation = lines.first().map {_.takeWhile {_ == ' '}.size()}.else {0}
    lux.add("pre") {
        lux.cssClass(Styles.guideCodeBlockCss)
        lux.add("code") {
            lux.cssClass(Styles.guideCodeCss)
            let unindentedCode = lines.map {_.dropFirst(indentation)}.join("\n")
            if(firefly) {
                renderHighlightedCode(lux, unindentedCode)
            } else {
                lux.text(unindentedCode)
            }
        }
    }
}

renderBullets(lux: Lux, items: List[List[Inline]]) {
    lux.add("ul") {
        items.each {inlines =>
            lux.add("li") {
                renderInlines(lux, inlines)
            }
        }
    }
}

renderImage(lux: Lux, url: String) {
    lux.add("img") {
        lux.set("src", url)
        lux.css(Css.maxWidth("100%"))
        lux.css(Css.borderRadius("8px"))
    }
}

renderVideo(lux: Lux, url: String) {
    lux.add("video") {
        lux.set("src", url)
        lux.css(Css.maxWidth("100%"))
    }
}

renderLuxDemo(lux: Lux, name: String, demos: List[Demo]) {
    demos.find {_.name == name}.map {_.render(lux)}.else {lux.text("Demo not found")}
}

renderInlines(lux: Lux, inlines: List[Inline]) {
    inlines.separate([Text(" ")]).each {renderInline(lux, _)}
}

renderInline(lux: Lux, inline: Inline) {
    inline.{
        | Anchor(heading, title) => 
            lux.add("a") {
                let url = title.map {"/guide/" + kebabCase(_)}.else {""} + "#" + kebabCase(heading)
                lux.cssClass(Styles.guideLinkCss)
                lux.set("href", url)
                lux.text(title.map {_ + ": "}.else {""} + heading)
            }
        | Bold(text) => 
            lux.add("b") {
                lux.text(text)
            }
        | Code(code, firefly) => 
            lux.add("code") {
                lux.cssClass(Styles.guideCodeCss)
                if(firefly) {
                    renderHighlightedCode(lux, code)
                } else {
                    lux.text(code)
                }
            }
        | Italic(text) => 
            lux.add("i") {
                lux.text(text)
            }
        | Link(text, url) => 
            lux.add("a") {
                lux.cssClass(Styles.guideLinkCss)
                lux.set("href", url)
                lux.text(text)
            }
        | Text(text) => 
            lux.text(text)
    }
}

renderHighlightedCode(lux: Lux, code: String) {
    try {
        Tokenizer.tokenize("<example>", code + "\n", None, False)
    }.toOption().map {tokens => 
        mutable offset = 0
        mutable index = 0
        lux.span {
            lux.cssClass(Styles.codeOtherCss)
            tokens.each {token =>
                if(token.startOffset > offset && index <= tokens.size() - 5) {
                    let text = code.slice(offset, token.startOffset)
                    if(text.all {c => c == ' ' || c == '\n'}) {
                        lux.text(text)
                    } else {
                        lux.span {
                            lux.cssClass(Styles.codeCommentCss)
                            lux.text(text)
                        }
                    }
                }
                if(token.kind != LEnd) {
                    let css = token.kind.{
                        | LChar => Styles.codeStringCss
                        | LFloat => Styles.codeNumberCss
                        | LInt => Styles.codeNumberCss
                        | LKeyword => Styles.codeKeywordCss
                        | LNamespace => Styles.codeTypeCss
                        | LString => Styles.codeStringCss
                        | LWildcard => Styles.codeVariableCss
                        | LUpper {tokens.get(index - 1).any {_.kind == LArrowThin}} => Styles.codeStringCss
                        | LUpper => Styles.codeTypeCss
                        | LLower {tokens.get(index - 1).any {_.kind == LArrowThin}} => Styles.codeStringCss
                        | LLower {tokens.get(index - 1).any {t => 
                            t.kind == LBracketRight || t.kind == LDot
                        }} => Styles.codeCallCss
                        | LLower {tokens.grab(index + 1).kind == LBracketLeft} => Styles.codeCallCss
                        | LLower => Styles.codeVariableCss
                        | _ => Styles.codeOtherCss
                    }
                    if(css == Styles.codeOtherCss) {
                        lux.text(code.slice(token.startOffset, token.stopOffset))
                    } else {
                        lux.span {
                            lux.cssClass(css)
                            lux.text(code.slice(token.startOffset, token.stopOffset))
                        }
                    }
                    offset = token.stopOffset
                }
                index += 1
            }
        }
    }.else {lux.text(code)}
}

kebabCase(text: String): String {
    mutable result = text.trim().lower().filter {c => c.isAsciiLetterOrDigit() || c == ' '}
    doWhile {
        let before = result
        result = result.replace("  ", "")
        result != before
    }
    result.replace(" ", "-")
}
