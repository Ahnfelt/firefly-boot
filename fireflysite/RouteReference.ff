import Lux from ff:lux
import WebServer from ff:webserver
import Router
import Guide
import ExamplesOverview
import FrontPage
import DocumentParser
import Html
import Main
import Website
import Menu

routeModule = SourceLocation.here().module()

data PageData(path: List[String], sections: List[Section])

handle(request: WebRequest[WebResponse], context: RouteContext, kebab: Option[String]) {
    let path = ["reference", ...kebab.toList()]
    let asset = "/assets/markdown/" + path.join("/") + ".md"
    try {context.system.assets().readText(asset)}.toOption().or {
        Log.trace("Asset not found: " + asset)
        request.writeText("404 Not Found")
        request.writeStatus("404 Not Found")
    }: markdown =>
    let parser = DocumentParser(path.last().grab(), markdown.split('\n'), 0)
    let sections = parser.parseDocument()
    let pageData = PageData(path, sections)
    let title = sections.grabFirst().heading
    //Log.debug(Show.show(pageData))
    Html.renderAndServe(context.system, routeModule, pageData, title, request, {render(_, pageData)})
}

browserMain(system: BrowserSystem) {
    Html.renderToMain(system, {render(_, _)})
}

render(lux: Lux, data: PageData) {
    let menuItem = Menu.findItem(Menu.menu, data.path).grab()
    Website.renderContentWithNext(lux, menuItem.path, menuItem.next) {lux =>
        Guide.renderSections(lux, data.sections, ExamplesOverview.demos())
    }
}
