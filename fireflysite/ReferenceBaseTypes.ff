import Guide

new(): Document {
    ReadyDocument([

        Section("Base types", [
            Paragraph([
                Text("Firefly has four primitive types: ")
                Code("String, Char, Int, Float.", firefly = True)
                Text("They have the following literal syntax:")
            ])
            CodeBlock("""
                "Hello, World!" // String
                'H' // Char
                42 // Int
                3.14 // Float
            """.replace("'''", "\"\"\""), firefly = True)
            Paragraph([
                Text("Strings may be concatenated using plus, e.g. ")
                Code("\"ban\" + \"ana\".", firefly = True)
                Text("String literals may contain escape sequences, e.g. ")
                Code("\\n, \\t, \\r, \\\", \\\'.")
                Text("You may also write multiline strings using triple quotes:")
                Code("\"\"\"...\"\"\".", firefly = True)
            ])
            Paragraph([
                Text("Ints support the usual arithmetic operators:")
                Code("+ - * / ^", firefly = True)
                Text("(division returns a Float, use the")
                Code(".div()", firefly = True)
                Text("method for integer division).")
            ])
            Paragraph([
                Text("Float literals may use scientific notation, e.g. ")
                Code("1.0e3, 1.0e-3", firefly = True)
                Text("The usual arithmetic operators are available:")
                Code("+ - * / ^", firefly = True)
            ])
        ])
        Section("Booleans", [
            Paragraph([
                Text("Bool is defined as a data type:")
            ])
            CodeBlock("""
                data Bool {
                    False
                    True
                }
            """, firefly = True)
            Paragraph([
                Text("Meaning it has two values:")
                Code("False, True.", firefly = True)
                Text("They support the usual logical operators:")
                Code("&& || !", firefly = True)
            ])
        ])
        Section("Durations and instants", [
            Paragraph([
                Text("Durations represent elapsed time, and instants represent points in time.")
            ])
            CodeBlock("""
                newtype Duration(seconds: Float)
                newtype Instant(since1970: Duration)
            """, firefly = True)
            Paragraph([
                Text("You can get the current instant:")
                Code("system.mainTask.now()", firefly = True)
                Text("or ask how much time elapsed since the program started:")
                Code("system.mainTask.elapsed().", firefly = True)
                Text("Note that neither has a time zone nor support for local calendars.")
            ])
        ])
        Section("Lists and arrays", [
            Paragraph([
                Text("List is the most common data structure in Firefly.")
                Text("It's an immutable contiguous blocks of values that can be indexed by integers, mapped over, filtered and so on.")
            ])
            CodeBlock("""
                let fruits: List[String] = 
                    ["apple", "banana", "orange"]
            """, firefly = True)
            Paragraph([
                Text("You can flatten one list into another using the spread syntax:")
                Code("[...friuts, \"cherry\"] == [\"apple\", \"banana\", \"orange\", \"cherry\"].", firefly = True)
            ])
            Paragraph([
                Text("Arrays is the mutable (and resizable) version of lists. You can convert a list to an array like this:")
                Code("fruits.toArray(),", firefly = True)
                Text("and you can construct an empty array like this:")
                Code("Array.new().", firefly = True)
            ])
        ])
        Section("Sets and maps", [
            Paragraph([
                Text("Set and Map are immutable and sorted collections. Sets have no duplicate entries, and maps have no duplicate keys.")
            ])
            CodeBlock("""
                let fruitSet: Set[String] = 
                    ["apple", "banana", "orange"].toSet()
                    
                let numberMap: Map[String, Int] = 
                    [Pair("one", 1), Pair("two", 2)].toMap()
            """, firefly = True)
            Paragraph([
                Text("They have methods for lookup, union, intersection, etc.")
                Text("In addition, there's a mutable IntMap and StringMap that's optimized for integer and string keys, respectively.")
            ])
        ])
        Section("Streams and buffers", [
            Paragraph([
                Text("Streams are lazy sequences that are consumed when read. They're often used for streaming I/O.")
            ])
            CodeBlock("""
                // Streaming file copy
                let stream = system.path("file1.txt").readStream()
                system.path("file2.txt").writeStream(stream)
            """, firefly = True)
            Paragraph([
                Text("Note that there is no risk of leaking a file handle above, as the file isn't opened until the first element of the stream is read,")
                Text("and the writeStream method will take care of closing the stream, even in the face of I/O errors.")
            ])
            Paragraph([
                Text("In this case you get a")
                Code("Stream[Buffer]", firefly = True)
                Text("back, which is common for streaming I/O in Firefly.")
                Text("A buffer is a mutable fixed-size array of bytes that you can manipulate with hardware friendly binary operations (8-bit, 16-bit, 32-bit etc.).")
            ])
        ])
        Section("Option, Pair, Unit and Nothing", [
            Paragraph([
                Text("Option represent a value that may be missing. There's no null in Firefly, so Option is often the return type of lookups. It's defined like this:")
            ])
            CodeBlock("""
                data Option[T] {
                    None
                    Some(value: T)
                }
            """, firefly = True)
            Paragraph([
                Text("Pair is a generic record with two fields, e.g. for key/value pairs in a map:")
            ])
            CodeBlock("""
                data Pair[A, B](first: A, second: B)
            """, firefly = True)
            Paragraph([
                Text("Unit is the return type for functions that have no interesting return value:")
            ])
            CodeBlock("""
                data Unit {
                    Unit
                }
            """, firefly = True)
            Paragraph([
                Text("Meaning it only has one possible value:")
                Code("Unit.", firefly = True)
            ])
            Paragraph([
                Text("Nothing is a type that has no values:")
            ])
            CodeBlock("""
                data Nothing {}
            """, firefly = True)
            Paragraph([
                Text("It's occasionally useful to statically rule out the possibility of constructing something.")
            ])
        ])
    ])
}
