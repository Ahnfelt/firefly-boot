import LspHook from ff:compiler
import Unification from ff:compiler
import Environment from ff:compiler
import Syntax from ff:compiler
import Handler
import CompletionHandler

handleSignatureHelp(system: NodeSystem, lspHook: LspHook): Json {
    let signatureAndUnification = lspHook.results().collectFirst {
        | InferLookupHook h => 
            let isMember = h.symbol.value.qualifiedName.contains("_")
            h.instantiated.value.map {i =>
                let signature = if(!isMember) {i.scheme.signature} else {
                    i.scheme.signature.Signature(parameters = i.scheme.signature.parameters.dropFirst())
                }
                Pair(signature, h.unification)
            }
        | _ => None
    }
    signatureAndUnification.map {| Pair(s, unification) => 
        let shownTypes = [...s.parameters.map {_.valueType}, s.returnType].map {unification.substitute(_)}
        let realGenerics = s.generics.filter {_ != "Q$"}
        let generics = if(realGenerics.isEmpty()) {""} else {"[" + realGenerics.join(", ") + "]"}
        // TODO only instantiate acording to self. Eg. Some("foo").map {...}
        let parameters = s.parameters.map {p => 
            CompletionHandler.showCompletionParameter("", p.Parameter(valueType = unification.substitute(p.valueType)))
        }
        let label = 
            s.name + generics + "(" + parameters.join(", ") + "): " + unification.substitute(s.returnType).show(shownTypes)
        Json.object()
            .with("signatures", [Json.object()
                .with("label", label)
                .with("parameters", [
                    ...parameters.map {p => 
                        Json.object().with("label", p)
                    }
                    // Fake parameter to trick the client into remembering the call position
                    Json.object().with("label", "/* Call " + lspHook.at.show() + " */")
                ])
            ])
            .with("activeSignature", 0)
    }.else {Json.null()}
}

pickActiveParameter(help: Json, argumentIndex: Int, parameterName: Option[String]): Json {
    parameterName.flatMap {name =>
        help.field("signatures").index(0).field("parameters").getArray().flatMap {parameters =>
            parameters.pairs().collectFirst {| Pair(i, p) => 
                if(name == p.field("label").grabString().takeWhile {_.isAsciiLetterOrDigit()}) {
                    help.with("activeParameter", i)
                }
            }
        }
    }.else {help.with("activeParameter", argumentIndex)}
}
