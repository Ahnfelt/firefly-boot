import LspHook from ff:compiler
import Unification from ff:compiler
import Environment from ff:compiler
import Syntax from ff:compiler
import Handler
import CompletionHandler

handleSignatureHelp(system: NodeSystem, lspHook: LspHook): JsValue {
    let signatureAndUnification = lspHook.results().collectFirst {
        | InferLookupHook h => 
            let isMember = h.symbol.value.qualifiedName.contains("_")
            h.instantiated.value.map {i =>
                let signature = if(!isMember) {i.scheme.signature} else {
                    i.scheme.signature.Signature(parameters = i.scheme.signature.parameters.dropFirst())
                }
                Pair(signature, h.unification)
            }
        | _ => None
    }
    let js = system.js()
    signatureAndUnification.map {| Pair(s, unification) => 
        let shownTypes = [...s.parameters.map {_.valueType}, s.returnType].map {unification.substitute(_)}
        let realGenerics = s.generics.filter {_ != "Q$"}
        let generics = if(realGenerics.isEmpty()) {""} else {"[" + realGenerics.join(", ") + "]"}
        // TODO only instantiate acording to self. Eg. Some("foo").map {...}
        let parameters = s.parameters.map {p => 
            CompletionHandler.showCompletionParameter("", p.Parameter(valueType = unification.substitute(p.valueType)))
        }
        let label = 
            s.name + generics + "(" + parameters.join(", ") + "): " + unification.substitute(s.returnType).show(shownTypes)
        js.object()
            .with("signatures", js.array([js.object()
                .with("label", label)
                .with("parameters", js.array([
                    ...parameters.map {p => 
                        js.object().with("label", p)
                    }
                    // Fake parameter to trick the client into remembering the call position
                    js.object().with("label", "/* Call " + lspHook.at.show() + " */")
                ]))
            ]))
            .with("activeSignature", 0)
    }.else {js.null()}
}

pickActiveParameter(help: JsValue, argumentIndex: Int, parameterName: Option[String]): JsValue {
    parameterName.flatMap {name =>
        let parameters = help.get("signatures").get(0).get("parameters").grabArray().toList()
        parameters.pairs().collectFirst {| Pair(i, p) => 
            if(name == p.get("label").grabString().takeWhile {_.isAsciiLetterOrDigit()}) {
                help.with("activeParameter", i)
            }
        }
    }.else {help.with("activeParameter", argumentIndex)}
}
