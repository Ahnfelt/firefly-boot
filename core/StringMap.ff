class StringMap[V] {}

new[V](): StringMap[V] {
    Js->Map->()?
}
    
extend self[V]: StringMap[V] {

    get(key: String): Option[V] {
        let result = self!->get(key)
        if(!result.isUndefined() || self!->has(key)?) {result?}
    }

    grab(key: String): V {
        let result = self!->get(key)
        if(result.isUndefined() && !self.has(key)) {throw(GrabException())}
        result?
    }
        
    set(key: String, value: V): Unit {
        self!->set(key, value!)
    }

    has(key: String): Bool {
        self!->has(key)?
    }

    remove(key: String): Bool {
        self!->delete(key)?
    }

    clear(): Unit {
        self!->clear()
    }

    size(): Int {
        self!->size?
    }

    each(body: (String, V) => Unit): Unit {
        if(Js.inAsync()) {
            let iterator = self!->entries()
            mutable result = iterator->next()
            while {!result->done?} {
                let value = result->value
                body(value.get(0)?, value.get(1)?)
                result = iterator->next()
            }
        } else {
            self!->forEach(Js->{v, k => body(k?, v?)})
        }
    }
            
    eachWhile(body: (String, V) => Bool): Unit
        target js sync "for(const [k, v] of self_) if(!body_(k, v)) break"
        target js async "for(const [k, v] of self_) if(!await body_(k, v)) break"

    toArray(): Array[Pair[String, V]] {
        let array = Array.new()
        self.each {k, v => array.push(Pair(k, v))}
        array
    }

    toList(): List[Pair[String, V]] {
        self.toArray().drain()
    }

    toStream(): Stream[Pair[String, V]] {
        self.toList().toStream()
    }

    toMap(): Map[String, V] {
        self.toList().toMap()
    }

    keys(): List[String] {
        let array = Array.new()
        self.each {k, v => array.push(k)}
        array.toList()
    }

    values(): List[V] {
        let array = Array.new()
        self.each {k, v => array.push(v)}
        array.toList()
    }

    copy(): StringMap[V] {
        Js->Map->(self!)?
    }

    getOrSet(key: String, body: () => V): V {
        if(!self.has(key)) {self.set(key, body())}
        self.grab(key)
    }
            
}

extend self[V]: StringMap[Array[V]] {

    push(key: String, value: V) {
        self.get(key).{
            | None => self.set(key, [value].toArray())
            | Some(array) => array.push(value)
        }
    }

}
