capability Lock(
    mutable owner: JsValue
    mutable level: Int
    queue: Queue[Pair[JsValue, Unit => Unit]]
)

capability LockCondition(
    lock: Lock
    queue: Queue[Unit => Unit]
)

extend self: Lock {

    condition(): LockCondition {
        LockCondition(self, Queue.new())
    }
    
    acquire(): Unit {
        if(self.level == 0 || self.owner === Js.currentTask()!) {
            self.owner = Js.currentTask()!
            self.level += 1
        } else {
            Js.awaitCancellablePromise {resolve, reject, onSettle =>
                let key = self.queue.push(Pair(Js.currentTask()!, resolve))
                onSettle {_ => self.queue.remove(key)}
            }
        }
    }
    
    release(): Unit {
        if(self.owner !== Js.currentTask()!) {
            throw(GrabException)
        } elseIf {self.level > 1} {
            self.level -= 1
        } else {
            self.owner = Js.undefined()
            self.level = 0
            if(!self.queue.isEmpty()) {
                let pending = self.queue.pop().grab()
                self.owner = pending.first
                self.level = 1
                pending.second(Unit)
            }
        }
    }
    
    do[T](body: () => T): T {
        self.acquire()
        try {
            body()
        } finally {
            self.release()
        }
    }

}

extend self: LockCondition {

    sleep(): Unit {
        if(self.lock.owner !== Js.currentTask()!) {
            throw(GrabException)
        }
        Js.throwIfCancelled()
        let level = self.lock.level
        self.lock.level = 1
        self.lock.release()
        try {
            Js.awaitCancellablePromise {resolve, reject, onSettle =>
                let key = self.queue.push(resolve)
                onSettle {_ => self.queue.remove(key)}
            }
        } finally {
            mutable error = Js.undefined()
            mutable acquired = False
            while {!acquired} {
                try {
                    self.lock.acquire()
                    self.lock.level = level
                    acquired = True
                } catchAny {e =>
                    error = e!
                }
            }
            if(error.isUndefined()) {Js.throw(error)}
        }
    }

    sleepUntil[T](body: () => Option[T]): T {
        body().{
            | Some(value) =>
                value
            | None =>
                self.sleep()
                tailcall self.sleepUntil(body)
        }
    }

    wakeOne(): Unit {
        self.queue.pop().each {resolve => resolve(Unit)}
    }

    wakeAll(): Unit {
        self.queue.each {_, resolve => resolve(Unit)}
        self.queue.clear()
    }

}
