trait T: Show {
    show(value: T): String
}

instance Bool: Show {
    show(value: Bool): String {
        | False => "False"
        | True => "True"
    }
}

instance Nothing: Show {
    show(value: Nothing): String {""}
}

instance Char: Show {
    show(value: Char): String { value.toString() }
}

instance Int: Show {
    show(value: Int): String { "" + value }
}

instance Float: Show {
    show(value: Float): String { "" + value }
}

instance String: Show {
    show(value: String): String
        target js sync "return JSON.stringify(value_);"
}

instance List[T: Show]: Show {
    show(value: List[T]): String {
        "[" + value.map(show).join(", ") + "]"
    }
}

instance Pair[A: Show, B: Show]: Show {
    show(value: Pair[A, B]): String {
        "Pair(" + show(value.first) + ", " + show(value.second) + ")"
    }
}
