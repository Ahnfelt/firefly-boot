if[T](condition: Bool, body: () => T): Option[T] {
    condition.{
        | False => None
        | True => Some(body())
    }
}

while(condition: () => Bool, body: () => Unit): Unit {
    condition().{
        | False =>
        | True =>
            body()
            tailcall while(condition, body)
    }
}

try[T](body: () => T): Try[T] { panic("magic") }
    javascript """
        try {
            return {Success: true, value_: body_()}
        } catch(e) {
            return {Failure: true, error_: e}
        }
    """
    javascriptasync """
        try {
            return {Success: true, value_: await body_($c)}
        } catch(e) {
            return {Failure: true, error_: e}
        }
    """

do[T](body: () => T): T {
    body()
}

throw[T](error: T) { panic("magic") }
    javascript "throw error_"

panic[T](message: String): T { panic("magic") }
    javascript "throw new Error(message_)"

magicShow[T](value: T): String { panic("magic") }
    //javascript "return ('' + value_)"
    javascript "return JSON.stringify(value_, undefined, 4)"
