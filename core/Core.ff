if[T](condition: Bool, body: () => T): Option[T] {
    condition.{
        | False => None
        | True => Some(body())
    }
}

while(condition: () => Bool, body: () => Unit): Unit {
    condition().{
        | False =>
        | True =>
            body()
            tailcall while(condition, body)
    }
}

try[T](body: () => T): Try[T] { panic("magic") }
    scala "scala.util.Try { body_() }"
    javascript """
        try {
            return {_: 'Success', value_: body_()}
        } catch(e) {
            return {_: 'Failure', exception_: e.message}
        }
    """

do[T](body: () => T): T {
    body()
}

panic[T](message: String): T { panic("magic") }
    scala "println(message_); throw new RuntimeException(message_)"
    javascript "throw new Error(message_)"

magicShow[T](value: T): String { panic("magic") }
    scala "value_.toString"
    javascript "return ('' + value_)"

magicHashCode[T](value: T): Int { panic("magic") }
    scala "value_.hashCode()"

magicLess[T](x: T, y: T): Bool { panic("magic") }
    scala "x_.asInstanceOf[Comparable[T]].compareTo(y_) < 0"
    javascript "return x_ < y_"

pipeDot(): Unit {}
    scala "}; def pipe_dot[A, B](value : A)(function : A => B) : B = { function(value)" // hack
