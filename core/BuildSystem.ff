capability BuildSystem {}

extend self: BuildSystem {

    compileForBrowser(mainFiles: List[String]): AssetSystem {
        // TODO: Check that the mainFile is in the current package directory
        internalCompile(self, mainFiles.map {internalPath(self, _)}, "browser")
        let streams = internalListDirectory(internalPath(self, ".firefly/output/browser"))
        let mainPackagePair = internalMainPackagePair(self)
        AssetSystem(streams.toMap())
    }

    bundleForBrowser(mainFiles: List[String], minify: Bool = True, sourceMaps: Bool = False): AssetSystem {
        internalCompile(self, mainFiles.map {internalPath(self, _)}, "browser")
        let browserOutputPath = internalPath(self, ".firefly/output/browser")
        let runPaths = internalListPath(browserOutputPath).filter {_.base().endsWith(".run.mjs")}.toList()
        let outputPath = runPaths.grabFirst().parent().grab()
        internalBrowserCallEsBuild(self, runPaths.map {_.absolute()}, outputPath.absolute(), minify, sourceMaps)
        let bundlePaths = internalListPath(browserOutputPath).filter {_.base().endsWith(".bundle.js")}.toList()
        AssetSystem(bundlePaths.map {p => 
            Pair("/" + p.relativeTo(browserOutputPath).replace("\\", "/"), {p.readStream()})
        }.toMap())
    }

    buildMode(): Bool {
        !!self!->"buildMode_"?
    }
    
    setAssets(assetSystem: AssetSystem): Unit {
        self!->"assets_" = assetSystem!
    }

    packageAssets(): AssetSystem {
        let streams = internalListDirectory(internalPath(self, "."))
        AssetSystem(streams.toMap())
    }

    dependencyAssets(user: String, package: String): AssetSystem {
        panic("dependencyAssets not yet implemented")
    }

    arguments(): List[String] {
        self!->"array_"?
    }

    mainTask(): Task {
        self!->"task_"?
    }

    crypto(): Crypto {
        Js->crypto?
    }
        
}

internalBrowserCallEsBuild(
    self: BuildSystem
    mainJsFiles: List[String]
    outputPath: String
    minify: Bool
    sourceMap: Bool
): Unit {
    let esbuild = Js.import("esbuild")
    Js.await(esbuild->build(Js->(
        entryPoints = mainJsFiles
        bundle = True
        minify = minify
        sourcemap = sourceMap
        platform = "browser"
        target = "es2017"
        outdir = outputPath
        outExtension = Js.object().with(".js", ".bundle.js")
    )))
}

internalNodeCallEsBuild(
    self: NodeSystem
    mainJsFile: String
    outputPath: String
    minify: Bool
): Unit {
    let esbuild = Js.import("esbuild")
    esbuild->build(Js->(
        entryPoints = [mainJsFile]
        bundle = True
        minify = minify
        sourcemap = True
        platform = "node"
        target = "es2017"
        external = ["../../../node_modules/*"] // TODO
        outfile = outputPath
    ))
}

internalListDirectory(path: Path): List[Pair[String, () => Stream[Buffer]]] {
    function go(currentPath: Path): Stream[Path] {
        currentPath.entries().flatMap {file =>
            if(file.isDirectory()) {
                go(file.path())
            } else {
                [file.path()].toStream()
            }
        }
    }
    go(path).map {file =>
        Pair("/" + file.relativeTo(path).replace("\\", "/"), {file.readStream()})
    }.toList()
}

internalListPath(path: Path): Stream[Path] {
    path.entries().flatMap {file =>
        if(file.isDirectory()) {
            internalListPath(file.path())
        } else {
            [file.path()].toStream()
        }
    }
}

internalPath(buildSystem: BuildSystem, absoluteOrRelative: String): Path {
    let nodePath = Js.import("path")
    Path(nodePath->resolve(absoluteOrRelative)?)
}

internalCompile(buildSystem: BuildSystem, mainFiles: List[Path], target: String): Unit {
    // Ported from the old FFI. It's quite fragile w.r.t. changes in code generation.
    Js.await(Js.rawIdentifier("$firefly_compiler")->"buildViaBuildSystem_$"(
        buildSystem! 
        internalPath(buildSystem, buildSystem!->"fireflyPath_"?)!
        mainFiles!
        target
        Js.currentTask()!
    ))
}

internalMainPackagePair(buildSystem: BuildSystem): Pair[String, String] {
    Pair(buildSystem!->"mainPackagePair_"->"group_"?, buildSystem!->"mainPackagePair_"->"name_"?)
}
