newtype Set[T](map: Map[T, Unit])

empty[T](): Set[T] {
    Set(Map.empty())
}

extend self[T: Order]: Set[T] {
    add(value: T): Set[T] { Set(self.map.add(value, Unit)) }
    addAll(that: Set[T]): Set[T] { Set(self.map.addAll(that.map)) }
    remove(value: T): Set[T] { Set(self.map.remove(value)) }
    removeAll(that: Set[T]): Set[T] { Set(self.map.removeAll(that.map)) }
    contains(value: T): Bool { self.map.contains(value) }
    size(): Int { self.map.size() }
    toList(): List[T] { self.map.pairs().map { _.first } }
    each(body: T => Unit): Unit { self.map.each {| k, _ => body(k)} }
}

instance Set[A: Order]: Order {
    compare(x: Set[A], y: Set[A]): Ordering {
        Ordering.compare(x.toList(), y.toList())
    }
}

instance Set[A: Show: Order]: Show {
    show(x: Set[A]): String {
        Show.show(x.toList()) + ".toSet()"
    }
}
