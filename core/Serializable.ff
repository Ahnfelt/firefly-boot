data DeserializationChecksumException()

serialize[T: Serializable](value: T, initialBufferSize: Int = 1024): Buffer {
    let serialization = Serialization(Buffer.make(initialBufferSize), 0, 0)
    serializeUsing(serialization, value)
    serialization.autoResize(4)
    serialization.buffer.setInt32(serialization.offset, serialization.checksum)
    serialization.offset += 4
    serialization.buffer.view(0, serialization.offset)
}

deserialize[T: Serializable](buffer: Buffer): T {
    let serialization = Serialization(buffer, 0, 0)
    let result = deserializeUsing(serialization)
    let checksum = serialization.buffer.grabInt32(serialization.offset)
    if(checksum != serialization.checksum) {
        throw(DeserializationChecksumException)
    }
    result
}

trait T: Serializable {
    serializeUsing(serialization: Serialization, value: T): Unit
    deserializeUsing(serialization: Serialization): T
}

data Serialization(
    mutable buffer: Buffer
    mutable offset: Int
    mutable checksum: Int
)

extend self: Serialization {
    autoResize(minSpareCapacity: Int) {
        if(self.offset + minSpareCapacity > self.buffer.size()) {
            let minSize = self.buffer.size() + minSpareCapacity
            let newBuffer = Buffer.make((self.buffer.size() * 2).max(minSize))
            newBuffer.setAll(0, self.buffer)
            self.buffer = newBuffer
        }
    }
}

instance Int: Serializable {
    serializeUsing(serialization: Serialization, value: Int): Unit {
        serialization.autoResize(8)
        serialization.buffer.setInt64(serialization.offset, value)
        serialization.offset += 8
    }
    deserializeUsing(serialization: Serialization): Int {
        let result = serialization.buffer.grabInt64(serialization.offset)
        serialization.offset += 8
        result
    }
}

instance Bool: Serializable {
    serializeUsing(serialization: Serialization, value: Bool): Unit {
        serialization.autoResize(1)
        serialization.buffer.setUint8(serialization.offset, if(value) {1} else {0})
        serialization.offset += 1
    }
    deserializeUsing(serialization: Serialization): Bool {
        let result = serialization.buffer.grabUint8(serialization.offset)
        serialization.offset += 1
        result == 1
    }
}

instance Array[T: Serializable]: Serializable {
    serializeUsing(serialization: Serialization, value: Array[T]): Unit {
        if(value.size() < 255) {
            serialization.autoResize(1)
            serialization.buffer.setUint8(serialization.offset, value.size())
            serialization.offset += 1
        } elseIf {value.size() < 1073741824} {
            serialization.autoResize(1 + 4)
            serialization.buffer.setUint8(serialization.offset, 255)
            serialization.buffer.setUint32(1 + serialization.offset, value.size())
            serialization.offset += 1 + 4
        } else {
            panic("Can't serialize arrays where size() >= 1073741824")
        }
        value.each {serializeUsing(serialization, _)}
    }
    deserializeUsing(serialization: Serialization): Array[T] {
        let smallSize = serialization.buffer.grabUint8(serialization.offset)
        if(smallSize != 255) {
            serialization.offset += 1
            Array.fillBy(smallSize) {_ => deserializeUsing(serialization)}
        } else {
            let size = serialization.buffer.grabUint32(serialization.offset + 1)
            serialization.offset += 1 + 4
            Array.fillBy(size) {_ => deserializeUsing(serialization)}
        }
    }
}

instance List[T: Serializable]: Serializable {
    serializeUsing(serialization: Serialization, value: List[T]): Unit {
        serializeUsing(serialization, value.toArray())
    }
    deserializeUsing(serialization: Serialization): List[T] {
        deserializeUsing[Array[T]](serialization).toList()
    }
}

instance Set[T: Serializable: Order]: Serializable {
    serializeUsing(serialization: Serialization, value: Set[T]): Unit {
        serializeUsing(serialization, value.toArray())
    }
    deserializeUsing(serialization: Serialization): Set[T] {
        deserializeUsing[Array[T]](serialization).toSet()
    }
}

instance Map[K: Serializable: Order, V: Serializable]: Serializable {
    serializeUsing(serialization: Serialization, value: Map[K, V]): Unit {
        serializeUsing(serialization, value.toArray())
    }
    deserializeUsing(serialization: Serialization): Map[K, V] {
        deserializeUsing[Array[Pair[K, V]]](serialization).toMap()
    }
}

instance String: Serializable {
    serializeUsing(serialization: Serialization, value: String): Unit {
        serialization.autoResize(1 + value.size())
        serialization.buffer.setUint8(serialization.offset, value.size())
        if(value.size() < 255 && internalSetLatin1(serialization.buffer, serialization.offset + 1, value)) {
            serialization.offset += 1 + value.size()
        } elseIf {value.size() < 1073741824} {
            let stringBuffer = value.toBuffer()
            serialization.autoResize(5 + stringBuffer.size())
            serialization.buffer.setUint8(serialization.offset, 255)
            serialization.buffer.setUint32(serialization.offset + 1, stringBuffer.size())
            serialization.buffer.setAll(serialization.offset + 5, stringBuffer)
            serialization.offset += 5 + stringBuffer.size()
        } else {
            panic("Can't serialize strings where size() >= 1073741824")
        }
    }
    deserializeUsing(serialization: Serialization): String {
        let smallSize = serialization.buffer.grabUint8(serialization.offset)
        if(smallSize != 255) {
            let result = internalGrabLatin1(serialization.buffer, serialization.offset + 1, smallSize)
            serialization.offset += 1 + smallSize
            result
        } else {
            let size = serialization.buffer.grabUint32(serialization.offset + 1)
            let stringBuffer = serialization.buffer.view(serialization.offset + 5, serialization.offset + 5 + size)
            serialization.offset += 5 + size
            stringBuffer.toString()
        }
    }
}


internalSetLatin1(self: Buffer, byteOffset: Int, value: String): Bool
    target js sync """
        for(let i = 0; i < value_.length; i++) {
            let charCode = value_.charCodeAt(i)
            if(charCode >= 256) return false
            self_.setUint8(byteOffset_ + i, charCode)
        }
        return true
    """

internalGrabLatin1(self: Buffer, byteOffset: Int, size: Int): String
    target js sync """
        const codeUnits = new Uint8Array(self_.buffer, self_.byteOffset + byteOffset_, size_)
        return String.fromCharCode.apply(null, codeUnits)
    """
