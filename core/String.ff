data String {}

extend self: String {

    size(): Int {
        self!->length?
    }

    grab(index: Int): Char {
        if(index < 0 || index >= self.size()) {
            throw(GrabException())
        }
        self!->charCodeAt(index)?
    }
    
    replace(needle: String, replacement: String): String {
        self!->replaceAll(needle, replacement)?
    }

    replaceFirst(needle: String, replacement: String): String {
        self!->replace(needle, replacement)?
    }

    reverse(): String {
        self!->split("")->reverse()->join("")?
    }

    repeat(count: Int): String {
        self!->repeat(count)?
    }

    trim(): String {
        self!->trim()?
    }

    trimStart(): String {
        self!->trimStart()?
    }

    trimEnd(): String {
        self!->trimEnd()?
    }

    lower(): String {
        self!->toLowerCase()?
    }

    upper(): String {
        self!->toUpperCase()?
    }

    dropWhile(p: Char => Bool): String {
        mutable i = 0
        while {i < self.size() && p(self!->charCodeAt(i)?)} {
            i += 1
        }
        self!->slice(i)?
    }

    takeWhile(p: Char => Bool): String {
        mutable i = 0
        while {i < self.size() && p(self!->charCodeAt(i)?)} {
            i += 1
        }
        self!->slice(0, i)?
    }

    slice(from: Int, until: Int): String {
        self!->slice(from, until)?
    }

    split(char: Char): List[String] {
        self!->split(Js->String->fromCharCode(char))?
    }
    
    splitFirst(char: Char): Option[Pair[String, String]] {
        let array: List[String] = self!->split(Js->String->fromCharCode(char), 2)?
        if(array.size() == 2) {
            Pair(array!.get(0)?, array!.get(1)?)
        }
    }    

    lines(): List[String] {
        self!->split(
            Js->RegExp->("[" + Js->String->fromCharCode(13)? + "]?[" + Js->String->fromCharCode(10)? + "]", "g")
        )?
    }

    dropFirst(count: Int = 1): String {
        self!->slice(count)?
    }

    dropLast(count: Int = 1): String {
        self!->slice(0, self.size() - count)?
    }
    
    getInt(): Option[Int] {
        if(self.size() > 0 && self.all {c => 48 <= c.codeUnit && c.codeUnit <= 57}) {
            Js->parseInt(self!, 10)?
        }
    }
            
    getFloat(): Option[Float] {
        // TODO return None for invalid input
        let result = Js->parseFloat(self!, 10)
        if(Js->isFinite(result)?) {result?}
    }

    grabInt(): Int {
        self.getInt().else {
            throw(GrabException())
        }
    }
        
    grabFloat(): Float {
        self.getFloat().else {
            throw(GrabException())
        }
    }

    first(): Option[Char] {
        if(self.size() > 0) {
            Char(self!->charCodeAt(0)?)
        }
    }

    last(): Option[Char] {
        if(self.size() > 0) {
            Char(self!->charCodeAt(self.size() - 1)?)
        }
    }

    grabFirst(): Char {
        self.first().else {
            throw(GrabException())
        }
    }

    grabLast(): Char {
        self.last().else {
            throw(GrabException())
        }
    }

    contains(substring: String): Bool {
        self!->includes(substring)?
    }

    startsWith(prefix: String, offset: Int = 0): Bool {
        self!->startsWith(prefix, offset)?
    }

    endsWith(prefix: String): Bool {
        self!->endsWith(prefix)?
    }

    removeFirst(prefix: String): Option[String] {
        if(self.startsWith(prefix)) {Some(self.dropFirst(prefix.size()))} else {None}
    }

    removeLast(suffix: String): Option[String] {
        if(self.endsWith(suffix)) {Some(self.dropLast(suffix.size()))} else {None}
    }
    
    padStart(length: Int, padding: String = " "): String {
        self!->padStart(length, padding)?
    }

    padEnd(length: Int, padding: String = " "): String {
        self!->padEnd(length, padding)?
    }
    
    each(body: Char => Unit): Unit {
        mutable i = 0
        while {i < self.size()} {
            body(Char(self!->charCodeAt(i)?))
            i += 1
        }
    }

    eachWhile(body: Char => Bool): Unit {
        mutable i = 0
        while {i < self.size()} {
            if(body(Char(self!->charCodeAt(i)?))) {
                i += 1
            } else {
                i = self.size()
            }
        }
    }    
    
    all(body: Char => Bool): Bool {
        mutable result = True
        self.eachWhile {x =>
            result = body(x)
            result
        }
        result
    }

    any(body: Char => Bool): Bool {
        mutable result = False
        self.eachWhile {x =>
            result = body(x)
            (!result)
        }
        result
    }
    
    find(body: Char => Bool): Option[Char] {
        mutable result = None
        self.eachWhile {x =>
            if(body(x)) {
                result = Some(x)
                False
            } else {
                True
            }
        }
        result
    }

    indexOf(needle: String, position: Int = 0): Option[Int] {
        let index = self!->indexOf(needle, position)?
        if(index != -1) {index}
    }
    
    indexWhere(body: Char => Bool): Option[Int] {
        mutable i = -1
        mutable result = False
        self.eachWhile {x =>
            i += 1
            result = body(x)
            (!result)
        }
        if(result) {i}
    }    
    
    filter(body: Char => Bool): String {
        let result = Array.new()
        self.each {x =>
            if(body(x)) {
                result.push(x.toString())
            }
        }
        result.join()
    }    

    toBuffer(): Buffer {
        let encoded = Js->TextEncoder->()->encode(self)
        Js->DataView->(encoded->buffer, encoded->byteOffset, encoded->byteLength)?
    }
}
