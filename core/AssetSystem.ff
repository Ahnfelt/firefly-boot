type AssetSystem!(
    files: Map[String, Stream[Buffer]]
)

create(): AssetSystem {
    AssetSystem([].toMap())
}

extend self: AssetSystem {

    addAssets(path: String, assets: AssetSystem): AssetSystem {
        let prefix = if(path.endsWith("/")) {path.dropLast(1)} else {path}
        AssetSystem(
            self.files.addAll(
                assets.files.map {p, stream =>
                     Pair(prefix + p, stream)
                }
            )
        )
    }

    assets(path: String): AssetSystem {
        let prefix = if(path.endsWith("/")) {path} else {path + "/"}
        let streams = self.files.pairs().collect {
            | Pair(p, s) {p.startsWith(prefix)} => Some(Pair(p.dropFirst(prefix.size()), s))
            | _ => None
        }
        AssetSystem(streams.toMap())
    }

    asset(path: String): AssetSystem {
        let name = path.reverse().takeWhile {_ != '/'}.reverse()
        let streams = self.files.get(path).toList().map {s => Pair("/" + name, s)}
        AssetSystem(streams.toMap())
    }

    list(path: String): List[String] {
        let prefix = if(path.endsWith("/")) {path} else {path + "/"}
        self.files.pairs().collect {
            | Pair(p, _) {p.startsWith(prefix)} => Some(p.dropFirst(prefix.size()).takeWhile {_ != '/'})
            | _ => None
        }.distinct()
    }

    exists(path: String): Bool {
        let prefix = if(path.endsWith("/")) {path} else {path + "/"}
        self.files.contains(path) || self.files.pairs().any {_.first.startsWith(prefix)}
    }

    readStream(file: String): Stream[Buffer] {
        self.files.get(file).else {
            panic("Asset not found for readStream: " + file)
        }
    }

    readText(file: String): String {
        self.readStream(file).toString()
    }

}
