data Option[T] {
    None
    Some(value: T)
}

extend self[T]: Option[T] {
    
    else(body: () => T): T {
        self.{
            | None => body()
            | Some(value) => value
        }
    }

    elseIf(condition: () => Bool, body: () => T): Option[T] {
        self.{
            | None =>
                condition().{
                    | True => Some(body())
                    | False => None
                }
            | Some(_) => self
        }
    }

    orElse(body: () => Option[T]): Option[T] {
        self.{
            | None => body()
            | Some(_) => self
        }
    }

    or[R](noneBody: () => R, someBody: T => R): R {
        self.{
            | None => noneBody()
            | Some(value) => someBody(value)
        }
    }
    
    isEmpty(): Bool {
        self.{
            | None => True
            | Some(_) => False
        }
    }

    toList(): List[T] {
        self.{
            | None => []
            | Some(v) => [v]
        }
    }

    toArray(): Array[T] {
        self.toList().toArray()
    }

    toStream(cycle: Bool = False): Stream[T] {
        mutable next = self
        Stream.new {
            let result = next
            if(!cycle) {next = None}
            result
        }
    }

    filter(body: T => Bool): Option[T] {
        self.{
            | Some(v) {body(v)} => Some(v)
            | _ => None
        }
    }
    
    map[R](body: T => R): Option[R] {
        self.{
            | None => None
            | Some(v) => Some(body(v))
        }
    }

    flatMap[R](body: T => Option[R]): Option[R] {
        self.{
            | None => None
            | Some(v) => body(v)
        }
    }

    each(body: T => Unit): Unit {
        self.{
            | None =>
            | Some(v) => body(v)
        }
    }

    all(body: T => Bool): Bool {
        self.{
            | None => True
            | Some(v) => body(v)
        }
    }

    any(body: T => Bool): Bool {
        self.{
            | None => False
            | Some(v) => body(v)
        }
    }

    grab(): T {
        self.{
            | None => Try.internalThrowGrabException()
            | Some(v) => v
        }
    }

}

extend self[T: Equal]: Option[T] {
    contains(value: T): Bool {
        self.{
            | None => False
            | Some(v) => v == value
        }
    }
}

extend self[T]: Option[Option[T]] {

    flatten(): Option[T] {
        self.{
            | None => None
            | Some(v) => v
        }
    }

}
