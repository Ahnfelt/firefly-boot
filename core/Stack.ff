type Stack[T] {}

empty[T](): Stack[T]
    target js sync "return {array: []}"

extend self[T]: Stack[T] {

    isEmpty(): Bool
        target js sync "return self_.array.length === 0"

    size(): Int
        target js sync "return self_.array.length"

    get(index: Int): Option[T]
        target js sync """
            return index_ >= 0 && index_ < self_.array.length
                ? ff_core_Option.Some(self_.array[index_])
                : ff_core_Option.None()
        """

    expect(index: Int): T
        target js sync """
            if(index_ < 0 || index_ >= self_.array.length) {
                throw new Error('Index ' + index_ + ' is out of bounds in an array of size ' + self_.array.length)
            }
            return self_.array[index_]
        """

    expectFirst(): T { self.expect(0) }

    expectLast(): T { self.expect(self.size() - 1) }

    first(): Option[T]
        target js sync """
            return self_.array.length > 0
                ? ff_core_Option.Some(self_.array[0])
                : ff_core_Option.None()
        """

    last(): Option[T]
        target js sync """
            return self_.array.length > 0
                ? ff_core_Option.Some(self_.array[self_.array.length - 1])
                : ff_core_Option.None()
        """

    push(value: T): Unit
        target js sync "self_.array.push(value_)"

    pushAll(value: Stack[T]): Unit
        target js sync "self_.array.push(...value_.array)"

    pushArray(value: Array[T]): Unit
        target js sync "self_.array.push(...value_)"

    pop(): Option[T]
        target js sync """
            return self_.array.length > 0
                ? ff_core_Option.Some(self_.array.pop())
                : ff_core_Option.None()
        """

    set(index: Int, value: T): Unit
        target js sync """
            if(index < 0 || index > self.array.length) {
                throw new Error('Index ' + index_ + ' is out of bounds in an array of size ' + self_.array.length)
            }
            self_.array[index_] = value
        """

    modify(index: Int, body: T => T): Unit
        target js sync "self_.array[index_] = body_(self_.array[index_])"
        target js async "self_.array[index_] = await body_(self_.array[index_], $c)"

    each(body: T => Unit): Unit
        target js sync """
            return self_.array.forEach(body_);
        """
        target js async """
            for(let i = 0; i < self_.array.length; i++) {
                await body_(self_.array[i], $c)
            }
        """

    eachWhile(body: T => Bool): Unit
        target js sync "for(const value of self_.array) if(!body_(value)) break"
        target js async "for(const value of self_.array) if(!await body_(value, $c)) break"

    all(body: T => Bool): Bool {
        mutable result = True
        self.eachWhile {x =>
            result = result && body(x)
            result
        }
        result
    }

    any(body: T => Bool): Bool {
        mutable result = False
        self.eachWhile {x =>
            result = result || body(x)
            (!result)
        }
        result
    }

    drain(): Array[T]
        target js sync "const result = self_.array; self_.array = []; return result"

    toArray(start: Int = 0, end: Int = 9007199254740991): Array[T]
        target js sync """return self_.array.slice(start_, end_)"""

    toList(start: Int = 0, end: Int = 9007199254740991): List[T]
        target js sync """
            if(start_ < 0) start_ = self_.array.length + start_
            if(end_ < 0) end_ = self_.array.length + start_
            start_ = Math.max(0, Math.min(start_, self_.array.length))
            end_ = Math.max(0, Math.min(end_, self_.array.length))
            let result = ff_core_List.Empty();
            for(let i = end_ - 1; i >= start_; i--) {
                result = ff_core_List.Link(self_.array[i], result);
            }
            return result;
        """

    sortBy[S: Order](body: T => S): Unit {
        self.sortUsing {Ordering.compare(body(_), body(_))}
    }

    sortUsing(ordering: (T, T) => Ordering): Unit
        target js sync "self_.array.sort((x, y) => ff_core_Ordering.Ordering_toInt(ordering_(x, y)))"
        // TODO: Async version

}

extend self[T: Order]: Stack[T] {

    sort(): Unit {
        self.sortUsing(Ordering.compare)
    }

}

extend self: Stack[String] {

    join(separator: String = ""): String
        target js sync "return self_.array.join(separator_)"

}

instance Array[T: Show]: Show {
    show(array: Array[T]): String {
        let stack = Stack.empty()
        stack.push("[")
        array.each {x =>
            if(stack.size() > 1) {stack.push(", ")}
            stack.push(Show.show(x))
        }
        stack.push("].toStack()")
        stack.join()
    }
}
