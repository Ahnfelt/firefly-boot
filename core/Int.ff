data Int {}

extend self: Int {

    abs(): Int
        target js sync "return Math.abs(self_)"

    toFloat(): Float
        target js sync "return self_"

    bitNot(): Int
        target js sync "return ~self_;"

    bitOr(that: Int): Int
        target js sync "return self_ | that_;"

    bitAnd(that: Int): Int
        target js sync "return self_ & that_;"

    bitXor(that: Int): Int
        target js sync "return self_ ^ that_;"

    bitLeft(bits: Int = 1): Int
        target js sync "return self_ << bits_;"

    bitRight(bits: Int = 1, signed: Bool = True): Int
        target js sync "return signed_ ? self_ >> bits_ : self_ >>> bits_;"

    to(inclusiveEnd: Int): List[Int] {
        mutable result = []
        mutable n = inclusiveEnd
        while {self <= n} {
            result = [n, ...result]
            n -= 1
        }
        result
    }

    until(exclusiveEnd: Int): List[Int] {
        mutable result = []
        mutable n = exclusiveEnd
        while {self < n} {
            result = [n - 1, ...result]
            n -= 1
        }
        result
    }

    min(that: Int): Int {
        if(self < that) {self} else {that}
    }

    max(that: Int): Int {
        if(self > that) {self} else {that}
    }

    clamp(from: Int, to: Int): Int {
        if(self <= from) {from} elseIf {self >= to} {to} else {self}
    }
    
    padStart(length: Int, padding: String = " "): String {
        ("" + self).padStart(length, padding)
    }
    
    padEnd(length: Int, padding: String = " "): String {
        ("" + self).padEnd(length, padding)
    }

}
