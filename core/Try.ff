type Try[T] {
    Success(value: T)
    Failure(error: Error)
}

type Error {}

extend self: Error {

    rethrow[T](): T { panic("magic") }
        javascript "throw self_"

    name(): String { panic("magic") }
        javascript "return self_.name || ''"

    message(): String { panic("magic") }
        javascript "return self_.message || ''"

    stack(): String { panic("magic") }
        javascript "return self_.stack || ''"

}

extend self[T]: Try[T] {

    map[S](body: T => S): Try[S] {
        self.{
            | Success(value) => try { body(value) }
            | Failure(error) => Failure(error)
        }
    }

    flatMap[S](body: T => Try[S]): Try[S] {
        self.map(body).flatten()
    }

    else(body: () => T): T {
        self.{
            | Success(value) => value
            | Failure(_) => body()
        }
    }

    expect(): T {
        self.{
            | Success(value) => value
            | Failure(error) => error.rethrow()
        }
    }

    /*
    catch[E: Exception](body: (Error, E) => T): Try[T] {
        | Failure(error) {error.as[E]() | Some(e)} => try { body(e) }
        | _ => self
    }
    */

    finally(body: () => Unit): Try[T] {
        self.{
            | Success(value) => try { body(); value }
            | Failure(_) => body(); self
        }
    }

}

extend self[T]: Try[Try[T]] {

    flatten(): Try[T] {
        self.{
            | Success(Success(_)@t) => t
            | Success(Failure(_)@t) => t
            | Failure(error) => Failure(error)
        }
    }

}
