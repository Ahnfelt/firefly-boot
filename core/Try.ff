type Try[T] {
    Success(value: T)
    Failure(error: Error)
}

do[T](body: () => T): Try[T]
    target js sync """
        try {
            return {Success: true, value_: body_()}
        } catch(e) {
            return {Failure: true, error_: e}
        }
    """
    target js async """
        try {
            return {Success: true, value_: await body_($c)}
        } catch(e) {
            return {Failure: true, error_: e}
        }
    """

catch[T](body: () => T, catchBody: Error => T): T {
    Try.catch {
        body()
    } { error =>
        catchBody(error)
    }
}

finally[T](body: () => T, finallyBody: () => Unit): T {
    Try.do {
        body()
    } finally {
        finallyBody()
    } grab()
}

extend self[T]: Try[T] {

    map[S](body: T => S): Try[S] {
        self.{
            | Success(value) => Try.do { body(value) }
            | Failure(error) => Failure(error)
        }
    }

    flatMap[S](body: T => Try[S]): Try[S] {
        self.map(body).flatten()
    }

    else(body: () => T): T {
        self.{
            | Success(value) => value
            | Failure(_) => body()
        }
    }

    grab(): T {
        self.{
            | Success(value) => value
            | Failure(error) => error.rethrow()
        }
    }

    catch[E: ToFromAny](body: (E, Error) => T): Try[T] {
        self.{
            | Failure(error) {error.exception().flatMap(Any.fromAny) | Some(e)} => do { body(e, error) }
            | _ => self
        }
    }

    handle(body: Error => T): Try[T] {
        self.{
            | Failure(error) => Try.do { body(error) }
            | _ => self
        }
    }

    finally(body: () => Unit): Try[T] {
        self.{
            | Success(value) => Try.do { body(); value }
            | Failure(_) =>
                Try.do { body() }.{
                    | Success(_) => self
                    | Failure(error) => Failure(error)
                }
        }
    }

}

extend self[T]: Try[Try[T]] {

    flatten(): Try[T] {
        self.{
            | Success(Success(_)@t) => t
            | Success(Failure(_)@t) => t
            | Failure(error) => Failure(error)
        }
    }

}
