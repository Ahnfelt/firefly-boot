import Syntax
import Tokenizer
import Parser
import Resolver
import Inference
import Emitter

type Compiler(
    files: FileSystem
    outputPath: String
    packagePaths: Map[String, String]
    literalModuleNames: Map[String, List[String]]
    mutable parsedModules: Map[String, Module]
    mutable resolvedModules: Map[String, Module]
    mutable inferredModules: Map[String, Module]
    mutable emittedModules: Set[String]
)

make(
    files: FileSystem
    outputPath: String
    packagePaths: Map[String, String]
    literalModuleNames: Map[String, List[String]]
): Compiler {
    Compiler(
        files = files
        outputPath = outputPath
        packagePaths = packagePaths
        literalModuleNames = literalModuleNames
        parsedModules = mapOf()
        resolvedModules = mapOf()
        inferredModules = mapOf()
        emittedModules = setOf()
    )
}

coreImports: List[DImport] = [
/*    DImport(
        at = Location("<prelude>", 1, 1),
        alias = "Hello",
        package = Some(Pair("ff", "core")),
        directory = [],
        file = "Hello"
    )*/
]

extend self: Compiler {

    parse(packageName: String, moduleName: String): Module {
        self.parsedModules.get(packageName + ":" + moduleName).else:

        let packagePair = do {
            let array = packageName.split(':')
            Pair(array.expect(0), array.expect(1))
        }
        let packagePath = self.packagePaths.expect(packageName)
        let file = moduleName + ".ff"
        let code = self.files.readText(packagePath + "/" + file)
        let tokens = Tokenizer.tokenize(file, code)
        let module = Parser.make(packagePair, file, tokens).parseModule()
        let result = module.Module(
            imports = coreImports ++ module.imports
        )

        self.parsedModules = self.parsedModules.add(packageName + ":" + moduleName, result)
        result
    }

    imports(packageName: String, module: Module): List[Module] {
        module.imports.map { import =>
            let newPackageName = import.package.first + ":" + import.package.second
            let newModuleName = import.directory.map { _ + "/" }.join("") + import.file
            self.parse(newPackageName, newModuleName)
        }
    }

    resolve(packageName: String, moduleName: String): Module {
        self.resolvedModules.get(packageName + ":" + moduleName).else:

        let coreModule = self.parse("ff:core", "Core")
        let module = self.parse(packageName, moduleName)
        let otherModules = self.imports(packageName, module)
        let result = Resolver.make(coreModule).resolveModule(module, otherModules)

        self.resolvedModules = self.resolvedModules.add(packageName + ":" + moduleName, result)
        result
    }

    infer(packageName: String, moduleName: String): Module {
        self.inferredModules.get(packageName + ":" + moduleName).else:

        let coreModule = self.resolve("ff:core", "Core")
        let module = self.resolve(packageName, moduleName)
        let otherModules = self.imports(packageName, module).map {
            self.resolve(packageName, self.files.prefixName(_.file))
        }
        let instances = coreModule.instances ++ module.instances ++ otherModules.flatMap { _.instances }
        let result = Inference.make(instances).inferModule(coreModule, module, otherModules)

        self.inferredModules = self.inferredModules.add(packageName + ":" + moduleName, result)
        module // result
    }

    emit(packageName: String, moduleName: String): Unit {
        if(self.emittedModules.contains(packageName + ":" + moduleName)) {} else:
        self.emittedModules = self.emittedModules.add(packageName + ":" + moduleName)

        let module = self.infer(packageName, moduleName)
        self.imports(packageName, module).each { i =>
            let newPackageName = i.packagePair.first + ":" + i.packagePair.second
            self.emit(newPackageName, self.files.prefixName(i.file))
        }

        let packagePair = do {
            let array = packageName.split(':')
            Pair(array.expect(0), array.expect(1))
        }
        let emitted = Emitter.emitModule(packagePair, module)
        let file = self.outputPath + "/" + packageName.replace(":", "/") + "/" + moduleName + ".scala"
        self.files.writeText(file, emitted)
    }

}
