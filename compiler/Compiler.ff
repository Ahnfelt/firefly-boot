import Syntax
import Tokenizer
import Parser
import Resolver
import Deriver
import Inference
import JsEmitter
import Dictionaries
import Dependencies
import LspHook

capability Compiler(
    emitTarget: EmitTarget
    files: FileSystem
    time: TimeSystem
    compilerModulePath: Option[String]
    jsOutputPath: String
    packagePaths: Map[PackagePair, String]
    singleFilePackages: Set[PackagePair]
    virtualFiles: Map[String, String]
    lspHook: LspHook
    mutable parsedModules: Map[String, Module]
    mutable resolvedModules: Map[String, Module]
    mutable derivedModules: Map[String, Module]
    mutable inferredModules: Map[String, Module]
    mutable emittedModules: Set[String]
    mutable phaseDurations: List[Pair[String, Duration]]
    mutable phaseDurationDelta: Duration
)

make(
    emitTarget: EmitTarget
    files: FileSystem
    time: TimeSystem
    compilerModulePath: Option[String]
    jsOutputPath: String
    resolvedDependencies: ResolvedDependencies
    virtualFiles: Map[String, String]
    lspHook: LspHook
): Compiler {
    Compiler(
        emitTarget = emitTarget
        files = files
        time = time
        compilerModulePath = compilerModulePath
        jsOutputPath = jsOutputPath
        packagePaths = resolvedDependencies.packagePaths
        singleFilePackages = resolvedDependencies.singleFilePackages
        virtualFiles = virtualFiles
        lspHook = lspHook
        parsedModules = Map.empty()
        resolvedModules = Map.empty()
        derivedModules = Map.empty()
        inferredModules = Map.empty()
        emittedModules = Set.empty()
        phaseDurations = []
        phaseDurationDelta = Duration(0.0)
    )
}

fail[T](at: Location, message: String): T {
    panic(message + " " + at.show())
}

coreImports: List[DImport] =
    [
        "Any"
        "Array"
        "AssetSystem"
        "Atomic"
        "Bool"
        "BrowserSystem"
        "Buffer"
        "BuildSystem"
        "Channel"
        "Char"
        "Core"
        "Duration"
        "Equal"
        "Error"
        "FileHandle"
        "FileSystem"
        "Float"
        "HttpClient"
        "Instant"
        "Int"
        "IntMap"
        "JsValue"
        "JsSystem"
        "List"
        "Lock"
        "Log"
        "Map"
        "NodeSystem"
        "Nothing"
        "Option"
        "Ordering"
        "Pair"
        "Path"
        "Serializable"
        "Set"
        "Show"
        "Stack"
        "Stream"
        "String"
        "StringMap"
        "Task"
        "TimeSystem"
        "Try"
        "Unit"
    ].map {moduleName =>
        DImport(
            at = Location("<prelude>", 1, 1)
            alias = moduleName
            package = PackagePair("ff", "core")
            directory = []
            file = moduleName
        )
    }

extend self: Compiler {

    measure[T](phase: String, packagePair: PackagePair, moduleName: String, body: () => T): T {
        let start = Duration(self.time.elapsed().seconds - self.phaseDurationDelta.seconds)
        let result = body()
        let stop = Duration(self.time.elapsed().seconds - self.phaseDurationDelta.seconds)
        let duration = Duration(stop.seconds - start.seconds)
        self.phaseDurationDelta = Duration(self.phaseDurationDelta.seconds + duration.seconds)
        let text = phase + " " + packagePair.groupName() + "/" + moduleName
        self.phaseDurations = [Pair(text, duration), ...self.phaseDurations]
        result
    }

    printMeasurements(): Unit {
        let worst = self.phaseDurations.sortBy {(_.second.seconds + 1000000.0) + ""}.takeLast(5).reverse()
        worst.each {| Pair(text, duration) =>
            Log.debug(text + ":\t" + duration.show())
        }
    }

    parse(packagePair: PackagePair, moduleName: String, importedAt: Option[Location]): Module {
        let packageName = packagePair.groupName()
        self.parsedModules.get(packageName + ":" + moduleName).else:
        self.measure("Parse", packagePair, moduleName):
        let packagePath = self.packagePaths.get(packagePair).else {
            panic("Internal error - package path missing: " + packagePair.groupName())
        }
        let file = moduleName + ".ff"
        let path = FileSystem.relative(self.files.workingDirectory(), packagePath + "/" + file)
        let fixedPath = path.replace("\\", "/")
        let code = self.virtualFiles.get(fixedPath).else {
            importedAt.each {at => if(!self.files.exists(fixedPath)) {
                throw(CompileError(at, "Imported module not found: " + packageName + "/" + moduleName))
            }}
            self.files.readText(fixedPath)
        }
        let completionAt = if(self.lspHook.isEnabled() && self.lspHook.insertIdentifier) {self.lspHook.at}
        let tokens = Tokenizer.tokenize(fixedPath, code, completionAt, self.lspHook.isEnabled())
        let parser = Parser.make(packagePair, file, tokens, self.emitTarget != EmitBrowser, self.lspHook)
        let module = if(self.singleFilePackages.contains(packagePair)) {
            parser.parseModuleWithPackageInfo().module
        } else {
            parser.parseModuleWithoutPackageInfo()
        }
        let result = module.Module(
            imports = [...coreImports, ...module.imports]
        )
        self.parsedModules = self.parsedModules.add(packageName + ":" + moduleName, result)
        result
    }

    imports(module: Module): List[Module] {
        module.imports.map {import =>
            let newPackagePair = import.package
            let newModuleName = import.directory.map {_ + "/"}.join("") + import.file
            if(!self.packagePaths.contains(newPackagePair)) {
                throw(CompileError(import.at, "Missing dependency declaration for: " + newPackagePair.groupName()))
            }
            self.parse(newPackagePair, newModuleName, Some(import.at))
        }
    }

    resolve(packagePair: PackagePair, moduleName: String): Module {
        let packageName = packagePair.groupName()
        self.resolvedModules.get(packageName + ":" + moduleName).else:
        self.measure("Resolve", packagePair, moduleName):

        let module = self.parse(packagePair, moduleName, None)
        let otherModules = self.imports(module)
        let resolver = Resolver.make(self.lspHook)
        let result = resolver.resolveModule(module, otherModules)

        self.resolvedModules = self.resolvedModules.add(packageName + ":" + moduleName, result)
        result
    }

    derive(packagePair: PackagePair, moduleName: String): Module {
        let packageName = packagePair.groupName()
        self.derivedModules.get(packageName + ":" + moduleName).else:
        self.measure("Derive", packagePair, moduleName):

        let module = self.resolve(packagePair, moduleName)
        let result = Deriver.make().deriveModule(module)

        self.derivedModules = self.derivedModules.add(packageName + ":" + moduleName, result)
        result
    }

    infer(packagePair: PackagePair, moduleName: String): Module {
        let packageName = packagePair.groupName()
        self.inferredModules.get(packageName + ":" + moduleName).else:
        self.measure("Infer", packagePair, moduleName):

        let module = self.derive(packagePair, moduleName)
        let otherModules = self.imports(module).map {i =>
            self.derive(i.packagePair, FileSystem.prefixName(i.file))
        }
        let inference = Inference.make([module, ...otherModules], self.lspHook)
        let inferredModule = inference.inferModule(module, otherModules)
        let result = Dictionaries.make([module, ...otherModules]).processModule(inferredModule, otherModules)

        self.inferredModules = self.inferredModules.add(packageName + ":" + moduleName, result)
        result
    }

    emit(packagePair: PackagePair, moduleName: String, isMainModule: Bool): Unit {
        let packageName = packagePair.groupName()
        if(self.emittedModules.contains(packageName + ":" + moduleName)) {} else:
        self.measure("Emit", packagePair, moduleName):
        self.emittedModules = self.emittedModules.add(packageName + ":" + moduleName)

        let module = self.infer(packagePair, moduleName)
        let otherModules = self.imports(module).map {i =>
            let newModuleName = FileSystem.prefixName(i.file)
            self.emit(i.packagePair, newModuleName, isMainModule = False)
            self.infer(i.packagePair, newModuleName)
        }

        let js = JsEmitter.make([module, ...otherModules], self.emitTarget, isMainModule, self.compilerModulePath)
            .emitModule(packagePair, module)
        let jsPath = self.jsOutputPath + "/" + packagePair.groupName("/")
        let jsFile = jsPath + "/" + moduleName + ".mjs"
        self.files.createDirectories(jsPath)
        self.files.writeText(jsFile, js)
    }

}
