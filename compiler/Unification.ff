import Syntax
import IntMap

type Unification(
    mutable substitution: IntMap[Type],
    mutable constraints: Map[Int, Map[String, ConstraintGenerics]],
    mutable nextTypeVariableIndex: Int
    instances: Map[InstanceKey, InstanceValue]
)

type ConstraintGenerics(at: Location, generics: List[Type])
type InstanceKey(traitName: String, typeName: String)
type InstanceValue(generics: List[String], constraints: List[Constraint], traitType: Type)

make(instances: List[DInstance]): Unification {
    function fail[T](at: Location, message: String): T {
        panic(message + " " + at.show())
    }
    Unification(
        IntMap.empty()
        Map.empty()
        2 // Even type variables to avoid collision with the parser
        instances.map { definition =>
            definition.traitType.{
                | TConstructor(at, name, [TConstructor(_, typeName, _), ...]) =>
                     Pair(
                        InstanceKey(name, typeName)
                        InstanceValue(
                            generics = definition.generics
                            constraints = definition.constraints
                            traitType = definition.traitType
                        )
                    )
                | TConstructor(at, name, _) =>
                    fail(at, "Instance requires type arguments: " + name + "[]")
                | TVariable(at, i) =>
                    fail(at, "Unexpected type variable: $" + i)
            }
        }.toMap()
    )
}

extend self: Unification {

    fail[T](at: Location, message: String): T {
        panic(message + " " + at.show())
    }

    freshTypeVariable(at: Location): Type {
        let result = TVariable(at, self.nextTypeVariableIndex)
        self.nextTypeVariableIndex += 2
        result
    }

    instantiate(instantiation: Map[String, Type], type: Type): Type {
        | _, TConstructor(at, name, []) =>
            instantiation.get(name).{
                | Some(t) => t
                | None => type
            }
        | _, TConstructor(at, name, generics) =>
            TConstructor(at, name, generics.map { self.instantiate(instantiation, _) })
        | _, TVariable(_, i) { self.has(i) } =>
            self.instantiate(instantiation, self.get(i))
        | _, TVariable(_, i) =>
            type
    }
    
    constrain(at: Location, type: Type, constraintName: String, generics: List[Type]): Unit {
        type.{
            | TVariable(_, i) { self.has(i) } =>
                self.constrain(at, self.get(i), constraintName, generics)
            | TVariable(_, i) =>
                self.constraints.get(i).{
                    | None =>
                        self.constraints = self.constraints.add(
                            i, [Pair(constraintName, ConstraintGenerics(at, generics))].toMap()
                        )
                    | Some(map) =>
                        map.get(constraintName).{
                            | None =>
                                let newMap = map.add(constraintName, ConstraintGenerics(at, generics))
                                self.constraints = self.constraints.add(i, newMap)
                            | Some(ConstraintGenerics(_, generics2)) =>
                                generics.zip(generics2).each {| Pair(t1, t2) => self.unify(at, t1, t2) }
                        }
                }
            | TConstructor(_, name, generics2) =>
                self.instances.get(InstanceKey(constraintName, name)).{
                    | None =>
                        let g1 = if(generics.isEmpty()) { "" } else { "[...]" }
                        let g2 = if(generics2.isEmpty()) { "" } else { "[...]" }
                        self.fail(at, "No such instance: " + name + g2 + ": " + constraintName + g1)
                    | Some(definition) =>
                        let typeVariables = definition.generics.map {| _ => self.freshTypeVariable(at) }
                        let instantiation = definition.generics.zip(typeVariables).toMap()
                        let traitType1 = self.instantiate(instantiation, definition.traitType)
                        let traitType2 = TConstructor(at, constraintName, [type, ...generics])
                        self.unify(at, traitType1, traitType2)
                        definition.constraints.each { constraint =>
                            self.instantiate(instantiation, constraint.representation).{
                                | TConstructor(_, newConstraintName, newGenerics) =>
                                    self.constrain(at, type, newConstraintName, newGenerics)
                                | TVariable(_, i) =>
                                    self.fail(at, "Constraint can't be a type variable: $" + i)
                            }
                        }
                }
        }
    }

    get(index: Int): Type {
        self.substitution.expect(index).{
            | TVariable(_, i) { self.has(i) } =>
                let t = self.get(i)
                self.substitution.set(index, t)
                t
            | t => t
        }
    }

    has(index: Int): Bool {
        self.substitution.contains(index)
    }

    substitute(type: Type): Type {
        | TVariable(_, i) => if(self.has(i)) { self.substitute(self.get(i)) } else { type }
        | TConstructor t => t.TConstructor(generics = t.generics.map { t => self.substitute(t) })
    }

    unify(at: Location, t1: Type, t2: Type): Unit {
        | _, TVariable(_, i1), TVariable(_, i2) { i1 == i2 } =>
        | _, TVariable(_, i), _ { self.has(i) } => self.unify(at, self.get(i), t2)
        | _, _, TVariable(_, i) { self.has(i) } => self.unify(at, t1, self.get(i))
        | _, TVariable(_, i), _ => self.bind(at, i, t2)
        | _, _, TVariable(_, i) => self.bind(at, i, t1)
        | _, TConstructor(_, name1, generics1), TConstructor(_, name2, generics2) =>
            if(name1 != name2 || generics1.size() != generics2.size()) {
                self.fail(at, "Type mismatch: " + self.substitute(t1).show() + " vs. " + self.substitute(t2).show())
            }
            generics1.zip(generics2).each {| Pair(t1, t2) => self.unify(at, t1, t2) }
    }

    bind(at: Location, index: Int, type: Type): Unit {
        if(self.occursIn(index, type)) {
            self.fail(at, "Infinite type: $" + index + " = " + self.substitute(type).show())
        }
        self.substitution.set(index, type)
        self.constraints.get(index).each { map =>
            self.constraints = self.constraints.remove(index)
            map.pairs().each {| Pair(name, ConstraintGenerics(at2, generics)) =>
                self.constrain(at2, type, name, generics)
            }
        }
    }

    occursIn(index: Int, t: Type): Bool {
        | _, TVariable(_, i) { self.has(i) } => self.occursIn(index, self.get(i))
        | _, TVariable(_, i) => i == index
        | _, TConstructor(_, _, generics) => generics.any { t => self.occursIn(index, t) }
    }
    
}
