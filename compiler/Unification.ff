import Syntax

type Unification(
    mutable substitution: Map[Int, Type],
    mutable constraints: Map[Int, Map[String, ConstraintGenerics]],
    mutable nextUnificationVariableIndex: Int
    mutable instances: Map[InstanceKey, InstanceValue]
)

type ConstraintGenerics(at: Location, generics: List[Type])

type InstanceKey(traitName: String, typeName: String)

instance InstanceKey: Order {
    compare(x: InstanceKey, y: InstanceKey): Ordering {
        Ordering.compare(x.traitName, y.traitName).{
            | OrderingSame => Ordering.compare(x.typeName, y.typeName)
            | o => o
        }
    }
}

type InstanceValue(
    generics: List[String],
    constraints: List[Constraint],
    packageName: String,
    moduleName: String,
    traitName: String,
    typeArguments: List[Type]
)

make(modules: List[Module]): Unification {
    function fail[T](at: Location, message: String): T {
        panic(message + " " + at.show())
    }

    Unification(
        Map.empty()
        Map.empty()
        2 // Even type variables to avoid collision with the parser
        modules.flatMap { module =>
            let packageName = module.packagePair.first + ":" + module.packagePair.second
            let moduleName = module.file.dropLast(".ff".size())
            module.instances.map { definition =>
                let typeName = definition.typeArguments.expectFirst().{
                    | TConstructor(_, name, _, _) => name
                    | TVariable(_, i) => fail(definition.at, "Unexpected unification variable: $" + i)
                }
                Pair(
                    InstanceKey(definition.traitName, typeName)
                    InstanceValue(
                        generics = definition.generics
                        constraints = definition.constraints
                        packageName = packageName
                        moduleName = moduleName
                        traitName = definition.traitName
                        typeArguments = definition.typeArguments
                    )
                )
            }
        }.toMap()
    )
}

extend self: Unification {

    fail[T](at: Location, message: String): T {
        panic(message + " " + at.show())
    }

    withLocalInstances[T](instances: Map[InstanceKey, InstanceValue], body: () => T): T {
        let oldInstances = self.instances
        self.instances = self.instances.addAll(instances)
        try {
            body()
        } finally {
            self.instances = oldInstances
        } expect()
    }

    freshUnificationVariable(at: Location): Type {
        let result = TVariable(at, self.nextUnificationVariableIndex)
        self.nextUnificationVariableIndex += 2
        result
    }

    instantiate(instantiation: Map[String, Type], type: Type): Type {
        | _, TConstructor(at, name, [], _) =>
            instantiation.get(name).{
                | Some(t) => t
                | None => type
            }
        | _, TConstructor t =>
            t.TConstructor(generics = t.generics.map { self.instantiate(instantiation, _) })
        | _, TVariable(_, i) { self.get(i) | Some(t) } =>
            self.instantiate(instantiation, t)
        | _, TVariable(_, i) =>
            type
    }
    
    instantiateConstraint(instantiation: Map[String, Type], constraint: Constraint): Constraint {
        | _, Constraint(at, name, generics) =>
            Constraint(at, name, generics.map { self.instantiate(instantiation, _) })
    }

    constrain(at: Location, type: Type, constraintName: String, generics: List[Type]): Unit {
        type.{
            | TVariable(_, i) { self.get(i) | Some(t) } =>
                self.constrain(at, t, constraintName, generics)
            | TVariable(_, i) =>
                self.constraints.get(i).{
                    | None =>
                        self.constraints = self.constraints.add(
                            i, [Pair(constraintName, ConstraintGenerics(at, generics))].toMap()
                        )
                    | Some(map) =>
                        map.get(constraintName).{
                            | None =>
                                let newMap = map.add(constraintName, ConstraintGenerics(at, generics))
                                self.constraints = self.constraints.add(i, newMap)
                            | Some(ConstraintGenerics(_, generics2)) =>
                                generics.zip(generics2).each {| Pair(t1, t2) => self.unify(at, t1, t2) }
                        }
                }
            | TConstructor(_, name, generics2, _) =>
                self.instances.get(InstanceKey(constraintName, name)).{
                    | None =>
                        let g1 = if(generics.isEmpty()) { "" } else { "[...]" }
                        let g2 = if(generics2.isEmpty()) { "" } else { "[...]" }
                        self.fail(at, "No such instance: " + name + g2 + ": " + constraintName + g1)
                    | Some(definition) =>
                        let unificationVariables = definition.generics.map {| _ => self.freshUnificationVariable(at) }
                        let instantiation = definition.generics.zip(unificationVariables).toMap()
                        let traitType1 = self.instantiate(instantiation,
                            TConstructor(at, definition.traitName, definition.typeArguments, [])
                        )
                        let traitType2 = TConstructor(at, constraintName, [type, ...generics], [])
                        self.unify(at, traitType1, traitType2)
                        definition.constraints.each { constraint =>
                            self.instantiateConstraint(instantiation, constraint).{
                                | Constraint(_, constraintName, newGenerics) =>
                                    self.constrain(at, newGenerics.expectFirst(), constraintName, newGenerics.dropFirst())
                            }
                        }
                }
        }
    }

    get(index: Int): Option[Type] {
        self.substitution.get(index).map {
            | TVariable(_, i) { self.substitution.get(i) | Some(t) } =>
                self.substitution = self.substitution.add(index, t)
                t
            | t => t
        }
    }

    substitute(type: Type): Type {
        | TVariable(_, i) { self.get(i) | Some(t) } => self.substitute(t)
        | TVariable(_, _) => type
        | TConstructor t => t.TConstructor(generics = t.generics.map { t => self.substitute(t) })
    }

    unify(at: Location, t1: Type, t2: Type): Unit {
        | _, TVariable(_, i1), TVariable(_, i2) { i1 == i2 } =>
        | _, TVariable(_, i), _ { self.get(i) | Some(t) } => self.unify(at, t, t2)
        | _, _, TVariable(_, i) { self.get(i) | Some(t) } => self.unify(at, t1, t)
        | _, TVariable(_, i), _ => self.bind(at, i, t2)
        | _, _, TVariable(_, i) => self.bind(at, i, t1)
        | _, TConstructor(_, name1, generics1, _), TConstructor(_, name2, generics2, _) =>
            if(name1 != name2 || generics1.size() != generics2.size()) {
                self.fail(at, "Type mismatch: " + self.substitute(t1).show() + " vs. " + self.substitute(t2).show())
            }
            generics1.zip(generics2).each {| Pair(t1, t2) => self.unify(at, t1, t2) }
    }

    bind(at: Location, index: Int, type: Type): Unit {
        if(self.occursIn(index, type)) {
            self.fail(at, "Infinite type: $" + index + " = " + self.substitute(type).show())
        }
        self.substitution = self.substitution.add(index, type)
        self.constraints.get(index).each { map =>
            self.constraints = self.constraints.remove(index)
            map.pairs().each {| Pair(name, ConstraintGenerics(at2, generics)) =>
                self.constrain(at2, type, name, generics)
            }
        }
    }

    occursIn(index: Int, t: Type): Bool {
        | _, TVariable(_, i) { self.get(i) | Some(type) } => self.occursIn(index, type)
        | _, TVariable(_, i) => i == index
        | _, TConstructor(_, _, generics, _) => generics.any { t => self.occursIn(index, t) }
    }
    
}
