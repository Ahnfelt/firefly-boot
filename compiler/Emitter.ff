import Syntax

fail[T](at: Location, message: String): T {
    panic(message + " " + at.show)
}

emitModule(module: Module): String {
    let moduleNamespace = module.file.replace('\\', '/').reverse.takeWhile { _ != '/' }.reverse.takeWhile { _ != '.' }
    let parts = [
        ["package firefly"]
        ["import firefly.Firefly_Core._", ...module.imports.map { "import firefly." + _.file + "_._" }]
        ["object " + moduleNamespace + "_ {"]
        if(module.functions.exists { _.signature.name == "main" }) { [emitMain()] } else { [] }
        module.types.map(emitTypeDefinition)
        module.lets.map { emitLetDefinition(_) }
        module.functions.map { emitFunctionDefinition(_) }
        module.extends.pairs.map { pair => emitExtendImplicit(pair.second, pair.first) }
        module.traits.map(emitTraitDefinition)
        module.instances.map(emitInstanceDefinition)
        ["}"]
    ]
    module.extends.map { _.type }.flatMap(TConstructor?).find { t =>
        !module.types.exists { _.name == t.name }
    }.foreach { t =>
        fail(t.at, "Type not defined in this file: " + t)
    }
    parts.map { _.mkString("\n\n") }.join("\n") + "\n"
}

emitMain() {
    "def main(arguments : Array[String]) : Unit = main(new System(arguments))"
}

emitTypeMembers(name: String, lets: List[DLet], functions: List[DFunction]) {
    let strings =
        lets.map { emitLetDefinition(_) } ++
        functions.map { emitFunctionDefinition(_) }
    "object " + name + " {\n\n" + strings.mkString("\n\n") + "\n\n}"
}

emitTypeDefinition(definition: DType): String {
    let generics = emitTypeParameters(definition.generics)
    if(definition.variants.size == 1 && definition.variants.head.name == definition.name) {
        let fields = "(" + definition.commonFields.map(emitParameter).mkString(", ") + ")"
        "case class " + definition.name + generics + fields
    } else {
        let commonFields = if(definition.commonFields.isEmpty) {""} else {
            " {\n" + definition.commonFields.map(emitParameter).map { "    val " + _ + "\n" }.mkString + "}"
        }
        let variants = definition.variants.map { emitVariantDefinition(definition, _) }
        let head = "sealed abstract class " + definition.name + generics + " extends Product with Serializable"
        head + commonFields + variants.map { "\n" + _ }.mkString
    }
}

emitLetDefinition(definition: DLet, mutable: Bool = False): String {
    let typeAnnotation = emitTypeAnnotation(definition.variableType)
    let mutability = if(mutable) {"var"} else {"val"}
    mutability + " " + escapeKeyword(definition.name) + typeAnnotation + " = " + emitTerm(definition.value)
}

emitFunctionDefinition(definition: DFunction, suffix: String = ""): String {
    let signature = emitSignature(definition.signature, suffix)
    definition.body.{
        | Lambda(_, Link(matchCase, Empty)) {
            matchCase.patterns.all { | PVariable(_, None) => True | _ => False }
        } =>
            let body = emitStatements(matchCase.body)
            signature + " = {\n" + body + "\n}"
        | _ =>
            let tuple = "(" + definition.signature.parameters.map { escapeKeyword(_.name) }.mkString(", ") + ")"
            let cases = definition.body.cases.map(emitCase).mkString("\n")
            signature + " = " + tuple + " match {\n" + cases + "\n}"
    }
}

emitExtendImplicit(definition: DExtend, index: Int): String {
    let generics = emitTypeParameters(definition.generics)
    let implicits = emitConstraints(definition.constraints)
    let parameter = escapeKeyword(definition.name) + " : " + emitType(definition.type)
    let methods = definition.methods.map { emitFunctionDefinition(_) }.join("\n\n")
    "implicit class " + extractTypeName(definition.type) + "_extend" + index + generics +
    "(" + parameter + ")" + implicits + " {\n\n" + methods + "\n\n}"
}

emitTraitDefinition(definition: DTrait): String {
    let generics = emitTypeParameters(definition.generics)
    let implicits = emitConstraints(definition.constraints)
    let parameters = if(definition.generatorParameters.isEmpty) {""} else {
        "(" + definition.generatorParameters.map(emitParameter).mkString(", ") + ")"
    }
    let methods = if(definition.methods.isEmpty) {""} else {
        " {\n\nimport " + definition.name + "._\n\n" +
        definition.methods.map { signature =>
            let body = definition.methodDefaults.find { _.first == signature.name }.map { | Pair(_, lambda) =>
                " {\n" + emitStatements(ELambda(lambda.at, lambda)) + "\n}"
            }.orElse(definition.methodGenerators.find { _.first == signature.name }.map { | Pair(_, e) =>
                " {\n// TODO: Generate\n}"
            }).getOrElse {
                ""
            }
            emitSignature(signature, "_m") + body
        }.mkString("\n\n") + "\n\n}"
    }
    let methodWrappers = if(definition.methods.isEmpty) {""} else {
        " \n\n" + definition.methods.map { signature =>
            let t = TConstructor(definition.at, definition.name, definition.generics.map {
                TConstructor(definition.at, _, List())
            })
            emitSignature(signature.copy(
                generics = definition.generics ++ signature.generics,
                constraints = Constraint(t) :: definition.constraints ++ signature.constraints
            )) + " =\n    scala.Predef.implicitly[" + emitType(t) + "]." + escapeKeyword(signature.name) +
            "_m(" + signature.parameters.map { _.name }.map(escapeKeyword).join(", ") + ")"
        }.join("\n\n") + "\n\n"
    }
    "abstract class " + definition.name + generics + parameters + implicits + methods + "\n" +
    "object " + definition.name + " {" + methodWrappers + "}"
}

emitInstanceDefinition(definition: DInstance): String {
    let signature = emitSignature(Signature(
        definition.at,
        extractTypeName(definition.traitType) + "_" + definition.hashCode().abs,
        definition.generics,
        definition.constraints,
        List(),
        definition.traitType
    ))
    let methods = " {\n\nimport " + extractTypeName(definition.traitType) + "._\n\n" +
        definition.methods.map { emitFunctionDefinition(_, "_m") }.join("\n\n") + "\n\n}"
    let value = "new " + emitType(definition.traitType) + methods
    "implicit " + signature + " =\n    " + value
}

emitVariantDefinition(typeDefinition: DType, definition: Variant): String {
    let generics = emitTypeParameters(typeDefinition.generics)
    let allFields = typeDefinition.commonFields ++ definition.fields
    let fields = "(" + allFields.map(emitParameter).mkString(", ") + ")"
    "case class " + definition.name + generics + fields + " extends " + typeDefinition.name + generics
}

emitSignature(signature: Signature, suffix: String = ""): String {
    let generics = emitTypeParameters(signature.generics)
    let parameters = "(" + signature.parameters.map(emitParameter).mkString(", ") + ")"
    let implicits = if(signature.constraints.isEmpty) {""} else {
        "(implicit " + signature.constraints.zipWithIndex.map { | Pair(c, i) =>
            "i_" + i + " : " + emitType(c.representation)
        }.mkString(", ") + ")"
    }
    let returnType = emitTypeAnnotation(signature.returnType)
    "def " + escapeKeyword(signature.name) + suffix + generics + parameters + implicits + returnType
}

emitParameter(parameter: Parameter): String {
    let mutability = if(parameter.mutable) {"var "} else {""}
    let defaultValue = parameter.default.map { " = " + emitTerm(_) }.getOrElse("")
    mutability + escapeKeyword(parameter.name) + emitTypeAnnotation(parameter.valueType) + defaultValue
}

emitConstraints(constraints: List[Constraint]): String {
    if(constraints.isEmpty) {""} else:
    let pairs = constraints.map { _.representation }.map(emitType).zipWithIndex
    "(implicit " + pairs.map { | Pair(k, v) => "i_" + v + " : " + k }.mkString(", ") + ")"
}

emitTypeParameters(generics: List[String]) {
    if(generics.isEmpty) {""} else {"[" + generics.mkString(", ") + "]"}
}

emitTypeAnnotation(t: Type): String {
    | TVariable _ => ""
    | TConstructor _ => " : " + emitType(t)
}

emitType(type: Type): String {
    | TVariable(_, index) =>
        "$" + index
    | TConstructor t =>
        if(t.name.startsWith("Function$")) {
            emitType(t.copy(name = t.name.replace("$", "")))
        } elseIf {t.name.startsWith("Record$")} {
            "{" + t.name.split('$').drop(1).toList.zip(t.generics).map { | Pair(field, fieldType) =>
                "val " + escapeKeyword(field) + " : " + emitType(fieldType)
            }.mkString("; ") + "}"
        } else {
            let generics = if(t.generics.isEmpty) {""} else {"[" + t.generics.map(emitType).mkString(", ") + "]"}
            escapeResolved(t.name) + generics
        }
}

emitStatements(term: Term): String {
    | EFunctions(at, functions, body) =>
        let functionStrings = functions.map { f => emitFunctionDefinition(DFunction(at, f.signature, f.body)) }
        functionStrings.mkString("\n") + "\n" + emitStatements(body)
    | ELet(at, mutable, name, valueType, value, body) =>
        emitLetDefinition(DLet(at, name, valueType, value), mutable) + ";\n" + emitStatements(body)
    | ESequential(at, before, after) =>
        emitStatements(before) + ";\n" + emitStatements(after)
    | EAssign(at, operator, name, value) =>
        escapeKeyword(name) + " " + operator + "= " + emitTerm(value)
    | EAssignField(at, operator, record, field, value) =>
        emitTerm(record) + "." + escapeKeyword(field) + " " + operator + "= " + emitTerm(value)
    | _ => emitTerm(term)
}

emitTerm(term: Term): String {
    | EString(at, value) => value
    | EChar(at, value) => value
    | EInt(at, value) => value
    | EFloat(at, value) => value
    | EVariable(at, name, _, _) => escapeResolved(name)
    | EList(at, _, items) { items.all { !_.second } } =>
        "List(" + items.map { emitTerm(_.first) }.mkString(", ") + ")"
    | EList(at, _, items) =>
        "(List(" + items.map {
            | Pair(item, False) => "List(" + emitTerm(item) + ")"
            | Pair(item, True) => emitTerm(item)
        }.mkString(", ") + ").flatten)"
    | EVariant(at, name, typeArguments, arguments) =>
        let generics = if(typeArguments.isEmpty) {""} else {"[" + typeArguments.map(emitType).mkString(", ") + "]"}
        escapeResolved(name) + generics + "(" + arguments.toList.flatten.map(emitArgument).mkString(", ") + ")"
    | EVariantIs(at, name, typeArguments) =>
        let generics = if(typeArguments.isEmpty) {""} else {"[" + typeArguments.map(emitType).mkString(", ") + "]"}
        "({ case _w : " + escapeResolved(name) + generics + " => Some(_w); case _ => None() })"
    | ECopy(at, name, record, fields) =>
        let fieldCode = fields.map { f => escapeKeyword(f.name) + " = " + emitTerm(f.value) }.mkString(", ")
        emitTerm(record) + ".copy(" + fieldCode + ")"
    | EField(at, record, field) =>
        emitTerm(record) + "." + escapeKeyword(field)
    | ELambda(at, Lambda(_, Link(MatchCase(_, patterns, None, body), Empty))) {
        patterns.all { | PVariable _ => True | _ => False }
    } =>
        let parameters =
            patterns.map { | PVariable p => p.name.map(escapeKeyword).getOrElse("_") | _ => panic("!") }.mkString(", ")
        "{(" + parameters + ") =>\n" + emitStatements(body) + "\n}"
    | ELambda(at, Lambda(_, cases)) =>
        let casesString = cases.map(emitCase).mkString("\n")
        "{\n" + casesString + "\n}"
    | EPipe(at, value, function) =>
        "pipe_dot(" + emitTerm(value) + ")(" + emitTerm(function) + ")"
    | ECall(at, EVariable(_, operator, _, _), Empty, Link(value, Empty)) {!operator.head.isLetter} =>
        "(" + operator + emitArgument(value) + ")"
    | ECall(at, EVariable(_, operator, _, _), Empty, Link(left, Link(right, Empty))) {!operator.head.isLetter} =>
        "(" + emitArgument(left) + " " + operator + " " + emitArgument(right) + ")"
    | ECall(at, function, typeArguments, arguments) =>
        let generics = if(typeArguments.isEmpty) {""} else {"[" + typeArguments.map(emitType).mkString(", ") + "]"}
        emitTerm(function) + generics + "(" + arguments.map(emitArgument).mkString(", ") + ")"
    | ERecord(at, fields) =>
        if(fields.isEmpty) {"{}"} else {
            let list = fields.map { f => "val " + escapeKeyword(f.name) + " = " + emitTerm(f.value) }
            "new {\n" + list.mkString(";\n") + ";\n}"
        }
    | EWildcard(at, index) =>
        if(index == 0) {fail(at, "Unbound wildcard")}
        "_w" + index
    | _ =>
        "{\n" + emitStatements(term) + "\n}"
}

emitArgument(argument: Argument) {
    argument.name.map { name => escapeKeyword(name) + " = " }.getOrElse("") + emitTerm(argument.value)
}

emitCase(matchCase: MatchCase) {
    let patterns = matchCase.patterns.map(emitPattern).mkString(", ")
    let condition = matchCase.condition.map { "if " + emitTerm(_) + " " }.getOrElse("")
    "case (" + patterns + ") " + condition + "=>\n" + emitStatements(matchCase.body)
}

emitPattern(pattern: MatchPattern): String {
    | PVariable(at, name) =>
        name.map(escapeKeyword).getOrElse("_")
    | PVariant(at, name, patterns) =>
        escapeResolved(name) + "(" + patterns.map(emitPattern).mkString(", ") + ")"
    | PVariantAs(at, name, variable) =>
        variable.map(escapeKeyword).getOrElse("_") + " : " + escapeResolved(name)
    | PAlias(at, p, variable) =>
        escapeKeyword(variable) + " @ (" + emitPattern(p) + ")"
    | PList(at, _, items) =>
        "List(" + items.map {
            | Pair(item, False) => emitPattern(item)
            | Pair(item, True) => emitPattern(item) + " : _*"
        }.join(", ") + ")"
}

extractTypeName(type: Type): String {
    | TVariable(at, index) =>
        fail(at, "Unexpected type variable: $" + index)
    | TConstructor t =>
        t.name
}

escapeResolved(word: String) {
    let parts = word.split("[.]").toList.map(escapeKeyword).join(".")
    if(parts.startsWith("ff:core/Core.")) {
        parts.replace("ff:core/Core.", "Firefly_Core.")
    } else {
        parts.replace(".", "_.")
    }
}

escapeKeyword(word: String) {
    if(keywords(word)) {word + "_"} else {word}
}

keywords = Set(
    "abstract"
    "case"
    "catch"
    "class"
    "def"
    "do"
    "else"
    "extends"
    "false"
    "final"
    "finally"
    "for"
    "forSome"
    "if"
    "implicit"
    "import"
    "lazy"
    "match"
    "new"
    "null"
    "object"
    "override"
    "package"
    "private"
    "protected"
    "return"
    "sealed"
    "super"
    "this"
    "throw"
    "trait"
    "true"
    "try"
    "type"
    "val"
    "var"
    "while"
    "with"
    "yield"
    "scala"
    "java"
)
