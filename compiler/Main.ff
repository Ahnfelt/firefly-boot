import Tokenizer
import Parser
import Syntax
import Resolver
import Compiler
import Unification
import Builder
import Dependencies
import JsEmitter
import Inference
import ModuleCache
import LspHook
import DependencyLock

data MainCommand {
    BootstrapCommand
    RunCommand(mainPath: String, argument: List[String])
    BrowserCommand(mainPath: String)
    BuildCommand(mainPath: String)
    CheckCommand(filePath: String)
    SymbolsCommand(outPath: String, filePaths: List[String])
}

data CommandLineError(problem: String)

main(system: NodeSystem): Unit {

    let fireflyPath = detectFireflyPath(system)

    function buildScript(
        mainPath: Path
        mainPackagePair: PackagePair
        emitTarget: EmitTarget
        resolvedDependencies: ResolvedDependencies
    ): ModuleKey {
        let fixedPackagePaths = if(resolvedDependencies.packagePaths.contains(PackagePair("ff", "core"))) {
            resolvedDependencies.packagePaths
        } else {
            resolvedDependencies.packagePaths.add(PackagePair("ff", "core"), fireflyPath.slash("core"))
        }
        let compilerModulePath = if(emitTarget != EmitBrowser && emitTarget != EmitExecutable) {
            fireflyPath.slash("output").slash("js").slash("ff").slash("compiler/Builder.mjs")
        }
        let targetName = emitTarget.{
            | EmitBuild => "build"
            | EmitNode => "node"
            | EmitBrowser => "browser"
            | EmitExecutable => "executable"
        }
        let folders = mainPath.parent().grab().relativeListTo(fixedPackagePaths.grab(mainPackagePair))
        let name = mainPath.base().removeLast(".ff").grab()
        let moduleKey = ModuleKey(mainPackagePair, folders, name)
        Builder.build(
            system = system
            emitTarget = emitTarget
            mainModules = [moduleKey]
            resolvedDependencies = resolvedDependencies.ResolvedDependencies(packagePaths = fixedPackagePaths)
            compilerModulePath = compilerModulePath
            tempPath = system.path(".firefly").slash("temporary")
            jsOutputPath = system.path(".firefly").path("output").path(targetName)
            printMeasurements = False
            moduleCache = ModuleCache.new(0)
        )
        moduleKey
    }

    function runCommand(command: MainCommand) {
        | RunCommand(mainFile, arguments) =>
            let resolvedDependencies = Dependencies.process(
                system.httpClient()
                DependencyLock.new(system.mainTask())
                system.path(mainFile)
            )
            prepareFireflyDirectory(system.path("."))
            let mainPath = system.path(mainFile)
            let moduleKey = buildScript(mainPath, resolvedDependencies.mainPackagePair, EmitNode, resolvedDependencies)
            if(!importAndRun(system, fireflyPath, "node", moduleKey, arguments)) {
                let at = Location(system.path(mainFile).absolute(), 1, 1)
                throw(CompileError(at, "This module does not contain a 'nodeMain' function"))
            }

        | BrowserCommand(mainFile) =>
            let resolvedDependencies = Dependencies.process(
                system.httpClient()
                DependencyLock.new(system.mainTask())
                system.path(mainFile)
            )
            prepareFireflyDirectory(system.path("."))
            let mainPath = system.path(mainFile)
            buildScript(mainPath, resolvedDependencies.mainPackagePair, EmitBrowser, resolvedDependencies)
            bundleForBrowser(system, resolvedDependencies.mainPackagePair, mainPath.base())

        | BuildCommand(mainFile) =>
            let resolvedDependencies = Dependencies.process(
                system.httpClient()
                DependencyLock.new(system.mainTask())
                system.path(mainFile + ".ff")
            )
            prepareFireflyDirectory(system.path("."))
            let mainPath = system.path(mainFile)
            let moduleKey = buildScript(mainPath, resolvedDependencies.mainPackagePair, EmitBuild, resolvedDependencies)
            buildScript(mainPath, resolvedDependencies.mainPackagePair, EmitExecutable, resolvedDependencies)
            bundleForPkg(system, resolvedDependencies.mainPackagePair, mainPath.base())
            importAndRun(system, fireflyPath, "build", moduleKey, [])

        | CheckCommand(filePath) => 
            let errors = Builder.check(
                system = system
                fireflyPath = fireflyPath
                path = system.path(filePath)
                mustContain = None
                skipFiles = Set.new()
                virtualFiles = Map.new()
                cache = ModuleCache.new(1)
                dependencyLock = DependencyLock.new(system.mainTask())
                newVersion = 0
                lspHook = LspHook.disabled() 
                infer = True
            )
            if(!errors.isEmpty()) {throw(CompileErrors(errors.distinct()))}

        | BootstrapCommand =>
            let workingDirectory = system.path(".")
            let fakeLocation = Location("<core>", 0, 0)
            Builder.build(
                system = system
                emitTarget = EmitNode
                mainModules = [ModuleKey(PackagePair("ff", "compiler"), [], "Main")]
                resolvedDependencies = ResolvedDependencies(
                    mainPackagePair = PackagePair("ff", "compiler")
                    packages = [Pair(
                        PackagePair("ff", "core"), PackageInfo(DPackage(
                            fakeLocation, PackagePair("ff", "core"), Version(fakeLocation, 0, 0, 0), TargetNames(True, False)
                        ), [], [DInclude(fakeLocation, "node_modules")])
                    )].toMap() // Only used for includes currently
                    packagePaths = [
                        Pair(PackagePair("ff", "compiler"), workingDirectory.slash("compiler"))
                        Pair(PackagePair("ff", "core"), workingDirectory.slash("core"))
                    ].toMap()
                    singleFilePackages = [].toSet()
                )
                compilerModulePath = None
                tempPath = workingDirectory.slash("output").slash("temporary")
                jsOutputPath = workingDirectory.slash("output").slash("js")
                printMeasurements = True
                moduleCache = ModuleCache.new(0)
            )
            
        | SymbolsCommand(outPath, filePaths) =>
            let columns = filePaths.flatMap {filePath =>
                let path = system.path(filePath)
                let code = path.readText()
                let packagePair = PackagePair("script", "script")
                let moduleKey = ModuleKey(packagePair, [], path.base().removeLast(".ff").grab())
                let tokens = Tokenizer.tokenize(path.absolute(), code, None, False)
                let parser = Parser.new(moduleKey, tokens, True, LspHook.disabled())
                let module = parser.parseModuleWithPackageInfo().module
                makeSymbolColumns(module)
            }
            let rowCount = columns.sortBy {_.size()}.last().map {_.size()}.else {0}
            let tsv = 0.until(rowCount).map {i => columns.map {_.get(i).else {""}}}
            system.path(outPath).writeText(tsv.map {_.join("\t")}.join("\n") + "\n")
            
    }

    try {
        let command = parseCommandLine(system.arguments())
        runCommand(command)
    } tryCatch {| CommandLineError(message), _ =>
        Log.debug(message)
    } tryCatch {| CompileError(at, message), _ =>
        Log.debug(message)
        Log.debug(" at " + at.file.replace("./", "") + ":" + at.line + ":" + at.column)
    } catch {| CompileErrors(errors), _ =>
        errors.map {| CompileError(at, message) => 
            Log.debug(message)
            Log.debug(" at " + at.file.replace("./", "") + ":" + at.line + ":" + at.column)
        }
    }
}

usageString = """
usage: firefly <main-file> [<main-arguments>] | <command> [<command-arguments>]

These are the commands:
   run <main-file> [<main-arguments>]    Run the main file with the provided arguments
   browser <main-file>                   Compile the main file for the browser
   build <main-file>                     Build the main file
   check <firefly-file>                  Check the firefly source file for errors
   symbols <firefly-file>                Print a .tsv with the symbols of a firefly source file
   bootstrap                             Bootstrap the compiler
"""

parseCommandLine(arguments: List[String]): MainCommand {
    | [mainFile, ...mainArguments] {mainFile.removeLast(".ff") | Some(mainName)} =>
        RunCommand(mainFile, mainArguments)
    | ["run", ...runArguments] =>
        runArguments.{
            | [mainFile, ...mainArguments] {mainFile.removeLast(".ff") | Some(mainName)} =>
                RunCommand(mainFile, mainArguments)
            | _ => throw(CommandLineError(
                "You must specify a Firefly file (.ff) as first argument to run." + usageString
            ))
        }
    | ["browser", ...browserArguments] =>
        browserArguments.{
            | [mainFile] {mainFile.removeLast(".ff") | Some(mainName)} =>
                BrowserCommand(mainFile)
            | [_, _, ...] => throw(CommandLineError(
                "You must only specify a single argument to browser." + usageString
            ))
            | _ => throw(CommandLineError(
                "You must specify a Firefly file (.ff) as the argument to browser." + usageString
            ))
        }
    | ["build", ...buildArguments] =>
        buildArguments.{
            | [mainFile] {mainFile.removeLast(".ff") | Some(mainName)} =>
                BuildCommand(mainFile)
            | [_, _, ...] => throw(CommandLineError(
                "You must only specify a single argument to build." + usageString
            ))
            | _ => throw(CommandLineError(
                "You must specify a Firefly file (.ff) as the argument to build." + usageString
            ))
        }
    | ["check", ...checkArguments] =>
        checkArguments.{
            | [fileName] =>
                CheckCommand(fileName)
            | [_, _, ...] => throw(CommandLineError(
                "You must only specify a single argument to check." + usageString
            ))
            | [] => throw(CommandLineError(
                "You must specify a Firefly file (.ff) or directory as the argument to check." + usageString
            ))
        }
    | ["symbols", ...checkArguments] =>
        checkArguments.{
            | [outName, fileName, ...fileNames] {outName.endsWith(".tsv")} =>
                SymbolsCommand(outName, [fileName, ...fileNames])
            | _ => throw(CommandLineError(
                "You must specify a output file (.tsv) and 1+ Firefly files (.ff) to symbols." + usageString
            ))
        }
    | ["bootstrap", _] =>
        throw(CommandLineError("bootstrap takes no arguments" + usageString))
    | ["bootstrap"] => BootstrapCommand
    | [] => throw(CommandLineError("You must specify a command or a main file to run." + usageString))
    | [s, ...] => throw(CommandLineError("Unknown command '" + s + "'" + usageString))
}

bundleForPkg(system: NodeSystem, packagePair: PackagePair, mainFile: String) {
    let prefix = ".firefly/output/executable/"
    let mainJsFile = prefix + packagePair.groupName("/") + "/" + mainFile + ".mjs"
    let file = prefix + "Main.bundle.js"
    BuildSystem.internalNodeCallEsBuild(system, mainJsFile, outputPath = file, minify = False)
}

bundleForBrowser(system: NodeSystem, packagePair: PackagePair, mainFile: String) {
    let prefix = ".firefly/output/browser/"
    let mainJsFile = prefix + packagePair.groupName("/") + "/" + mainFile + ".mjs"
    let file = prefix + "Main.bundle.js"
    BuildSystem.internalCallEsBuild(system, mainJsFile, outputPath = file, minify = True, sourceMap = True)
}

importAndRun(
    system: NodeSystem
    fireflyPath: Path
    target: String
    moduleKey: ModuleKey
    arguments: List[String]
): Bool {
    let process = Js.await(Js.dynamicImport("process"))
    let cwd = process->cwd()
    let workingDirectory = if(cwd->indexOf(":") === 1) {"file:///" + cwd?} else {cwd?}
    let packagePath = moduleKey.packagePair.groupName("/")
    let runFile = workingDirectory + "/.firefly/output/" + target + "/" + packagePath + "/" + moduleKey.importName() + ".run.mjs"
    let runFilePath = if(runFile.contains("://")) {system.pathFromUrl(runFile)} else {system.path(runFile)}
    if(runFilePath.exists()) {
        let main = Js.await(Js.dynamicImport(runFile))
        main->"$run$"(fireflyPath.absolutePath, arguments)
        True
    } else {
        False
    }
}

prepareFireflyDirectory(path: Path) {
    if(!path.slash(".firefly").slash("output").exists()) {
        if(!path.slash(".firefly").exists()) {
            path.slash(".firefly").createDirectory()
        }
        path.slash(".firefly").slash("output").createDirectory()
    }
}

detectFireflyPath(system: NodeSystem): Path {
    let url = Js.import("url")
    let suffix = "/output/js/ff/compiler/Main.mjs"
    let moduleUrl = Js->import->meta->url
    if(!Js->import->meta->url->endsWith(suffix)?) {
        Js.throw(Js->Error->("Expected module path to end with: " + suffix + ", but got: " + moduleUrl?))
    } else {
        Path(url->fileURLToPath(Js->URL->(moduleUrl->slice(0, -suffix.size())))?)
    }
}

makeSymbolColumns(module: Module): List[List[String]] {
    function processSignature(signature: Signature): String {
        let generics = if(signature.generics.isEmpty()) {""} else {"[" + signature.generics.join(", ") + "]"}
        let parameters = "(" + signature.parameters.map {_.name}.join(", ") + ")"
        signature.name + generics + parameters
    }
    let traits = module.traits.map {x =>
        let generics = x.generics.dropFirst()
        let g = x.generics.first().map {_ + ": "}.else {""}
        (name = g + x.name, generics = generics, symbols = x.methods.map {processSignature(_)})
    }
    let types = module.types.map {x =>
        let variants = x.variants.map {variant =>
            let parameters = if(variant.fields.isEmpty() && x.commonFields.isEmpty()) {""} else {
                "(" + [...x.commonFields, ...variant.fields].map {_.name}.join(", ") + ")"
            }
            variant.name + parameters
        }
        let methods = module.extends.filter {_.type.{
            | TConstructor(_, name, _) => name == x.name
            | _ => False
        }}.flatMap {_.methods}.map {_.signature}.map {processSignature(_)}
        (name = x.name, generics = x.generics, symbols = [...variants, ...methods])
    }
    let toplevel = if(module.functions.isEmpty()) {[]} else {
        let functions = module.functions.map {processSignature(_.signature)}
        [(name = "", generics = [], symbols = [...module.lets.map {_.name}, ...functions])]
    }
    let all = [...toplevel, ...types, ...traits]
    all.map {r => 
        let generics = if(r.generics.isEmpty()) {""} else {"[" + r.generics.join(", ") + "]"}
        let header = (module.moduleKey.importName() + " " + r.name + generics).trim()
        [header, ...r.symbols.sort()]
    }
}
