import Tokenizer
import Parser
import Syntax
import Resolver
import Compiler
import Unification
import Builder
import Dependencies
import JsEmitter
import Inference
import ModuleCache
import LspHook
import DependencyLock

data MainCommand {
    BootstrapCommand
    RunCommand(mainPath: String, argument: List[String])
    BrowserCommand(mainPath: String)
    BuildCommand(mainPath: String)
    CheckCommand(filePath: String)
}

data CommandLineError(problem: String)

main(system: NodeSystem): Unit {

    let fireflyPath = detectFireflyPath(system)

    function buildScript(
        mainFile: String
        mainPackagePair: PackagePair
        emitTarget: EmitTarget
        resolvedDependencies: ResolvedDependencies
    ) {
        let fixedPackagePaths = if(resolvedDependencies.packagePaths.contains(PackagePair("ff", "core"))) {
            resolvedDependencies.packagePaths
        } else {
            resolvedDependencies.packagePaths.add(PackagePair("ff", "core"), fireflyPath.slash("core"))
        }
        let compilerModulePath = if(emitTarget != EmitBrowser && emitTarget != EmitExecutable) {
            fireflyPath.slash("output").slash("js").slash("ff").slash("compiler/Builder.mjs")
        }
        let targetName = emitTarget.{
            | EmitBuild => "build"
            | EmitNode => "node"
            | EmitBrowser => "browser"
            | EmitExecutable => "executable"
        }
        Builder.build(
            system = system
            emitTarget = emitTarget
            mainPackage = mainPackagePair
            mainModule = mainFile
            resolvedDependencies = resolvedDependencies.ResolvedDependencies(packagePaths = fixedPackagePaths)
            compilerModulePath = compilerModulePath
            tempPath = system.path(".firefly").slash("temporary")
            jsOutputPath = system.path(".firefly").path("output").path(targetName)
            printMeasurements = False
            moduleCache = ModuleCache.new(0)
        )
    }

    function runCommand(command: MainCommand) {
        | RunCommand(mainFile, arguments) =>
            let resolvedDependencies = Dependencies.process(
                system.httpClient()
                DependencyLock.new(system.mainTask())
                system.path(mainFile + ".ff")
            )
            prepareFireflyDirectory(system.path("."))
            let localMainFile = system.path(mainFile).base()
            buildScript(localMainFile, resolvedDependencies.mainPackagePair, EmitNode, resolvedDependencies)
            if(!importAndRun(fireflyPath, "node", resolvedDependencies.mainPackagePair, localMainFile, arguments)) {
                let at = Location(system.path(mainFile + ".ff").absolute(), 1, 1)
                throw(CompileError(at, "This module does not contain a 'nodeMain' function"))
            }

        | BrowserCommand(mainFile) =>
            let resolvedDependencies = Dependencies.process(
                system.httpClient()
                DependencyLock.new(system.mainTask())
                system.path(mainFile + ".ff")
            )
            prepareFireflyDirectory(system.path("."))
            let localMainFile = system.path(mainFile).base()
            buildScript(mainFile, resolvedDependencies.mainPackagePair, EmitBrowser, resolvedDependencies)
            bundleForBrowser(system, resolvedDependencies.mainPackagePair, localMainFile)

        | BuildCommand(mainFile) =>
            let resolvedDependencies = Dependencies.process(
                system.httpClient()
                DependencyLock.new(system.mainTask())
                system.path(mainFile + ".ff")
            )
            prepareFireflyDirectory(system.path("."))
            let localMainFile = system.path(mainFile).base()
            buildScript(localMainFile, resolvedDependencies.mainPackagePair, EmitBuild, resolvedDependencies)
            buildScript(localMainFile, resolvedDependencies.mainPackagePair, EmitExecutable, resolvedDependencies)
            bundleForPkg(system, resolvedDependencies.mainPackagePair, localMainFile)
            importAndRun(fireflyPath, "build", resolvedDependencies.mainPackagePair, localMainFile, [])

        | CheckCommand(filePath) => 
            let errors = Builder.check(
                system = system
                fireflyPath = fireflyPath
                path = system.path(filePath)
                mustContain = None
                skipFiles = Set.new()
                virtualFiles = Map.new()
                cache = ModuleCache.new(1)
                dependencyLock = DependencyLock.new(system.mainTask())
                newVersion = 0
                lspHook = LspHook.disabled() 
                infer = True
                checkDependencies = False
            )
            if(!errors.isEmpty()) {throw(CompileErrors(errors.distinct()))}

        | BootstrapCommand =>
            let workingDirectory = system.path(".")
            let fakeLocation = Location("<core>", 0, 0)
            Builder.build(
                system = system
                emitTarget = EmitNode
                mainPackage = PackagePair("ff", "compiler")
                mainModule = "Main"
                resolvedDependencies = ResolvedDependencies(
                    mainPackagePair = PackagePair("ff", "compiler")
                    packages = [Pair(
                        PackagePair("ff", "core"), PackageInfo(DPackage(
                            fakeLocation, PackagePair("ff", "core"), Version(fakeLocation, 0, 0, 0), TargetNames(True, False)
                        ), [], [DInclude(fakeLocation, "node_modules")])
                    )].toMap() // Only used for includes currently
                    packagePaths = [
                        Pair(PackagePair("ff", "compiler"), workingDirectory.slash("compiler"))
                        Pair(PackagePair("ff", "core"), workingDirectory.slash("core"))
                    ].toMap()
                    singleFilePackages = [].toSet()
                )
                compilerModulePath = None
                tempPath = workingDirectory.slash("output").slash("temporary")
                jsOutputPath = workingDirectory.slash("output").slash("js")
                printMeasurements = True
                moduleCache = ModuleCache.new(0)
            )
    }

    try {
        let command = parseCommandLine(system.arguments())
        runCommand(command)
    } tryCatch {| CommandLineError(message), _ =>
        Log.debug(message)
    } tryCatch {| CompileError(at, message), _ =>
        Log.debug(message)
        Log.debug(" at " + at.file.replace("./", "") + ":" + at.line + ":" + at.column)
    } catch {| CompileErrors(errors), _ =>
        errors.map {| CompileError(at, message) => 
            Log.debug(message)
            Log.debug(" at " + at.file.replace("./", "") + ":" + at.line + ":" + at.column)
        }
    }
}

usageString = """
usage: firefly <main-file> [<main-arguments>] | <command> [<command-arguments>]

These are the commands:
   run <main-file> [<main-arguments>]    Run the main file with the provided arguments
   browser <main-file>                   Compile the main file for the browser
   build <main-file>                     Build the main file
   check <firefly-file>                  Check the firefly source file for errors
   bootstrap                             Bootstrap the compiler
"""

parseCommandLine(arguments: List[String]): MainCommand {
    | [mainFile, ...mainArguments] {mainFile.removeLast(".ff") | Some(mainName)} =>
        RunCommand(mainName, mainArguments)
    | ["run", ...runArguments] =>
        runArguments.{
            | [mainFile, ...mainArguments] {mainFile.removeLast(".ff") | Some(mainName)} =>
                RunCommand(mainName, mainArguments)
            | _ => throw(CommandLineError(
                "You must specify a Firefly file (.ff) as first argument to run."  + usageString
            ))
        }
    | ["browser", ...browserArguments] =>
        browserArguments.{
            | [mainFile] {mainFile.removeLast(".ff") | Some(mainName)} =>
                BrowserCommand(mainName)
            | [_, _, ...] => throw(CommandLineError(
                "You must only specify a single argument to browser."  + usageString
            ))
            | _ => throw(CommandLineError(
                "You must specify a Firefly file (.ff) as the argument to browser."  + usageString
            ))
        }
    | ["build", ...buildArguments] =>
        buildArguments.{
            | [mainFile] {mainFile.removeLast(".ff") | Some(mainName)} =>
                BuildCommand(mainName)
            | [_, _, ...] => throw(CommandLineError(
                "You must only specify a single argument to build."  + usageString
            ))
            | _ => throw(CommandLineError(
                "You must specify a Firefly file (.ff) as the argument to build."  + usageString
            ))
        }
    | ["check", ...checkArguments] =>
        checkArguments.{
            | [fileName] =>
                CheckCommand(fileName)
            | [_, _, ...] => throw(CommandLineError(
                "You must only specify a single argument to check."  + usageString
            ))
            | [] => throw(CommandLineError(
                "You must specify a Firefly file (.ff) or directory as the argument to check."  + usageString
            ))
        }
    | ["bootstrap", _] =>
        throw(CommandLineError("bootstrap takes no arguments" + usageString))
    | ["bootstrap"] => BootstrapCommand
    | [] => throw(CommandLineError("You must specify a command or a main file to run." + usageString))
    | [s, ...] => throw(CommandLineError("Unknown command '" + s + "'" + usageString))
}

bundleForPkg(system: NodeSystem, packagePair: PackagePair, mainFile: String) {
    let prefix = ".firefly/output/executable/"
    let mainJsFile = prefix + packagePair.groupName("/") + "/" + mainFile + ".mjs"
    let file = prefix + "Main.bundle.js"
    BuildSystem.internalNodeCallEsBuild(system, mainJsFile, outputPath = file, minify = False)
}

bundleForBrowser(system: NodeSystem, packagePair: PackagePair, mainFile: String) {
    let prefix = ".firefly/output/browser/"
    let mainJsFile = prefix + packagePair.groupName("/") + "/" + mainFile + ".mjs"
    let file = prefix + "Main.bundle.js"
    let browserCode = BrowserCode(
        packageGroup = packagePair.group
        packageName = packagePair.name
        mainFile = system.path(mainFile)
        assetSystem = AssetSystem([Pair("/", {system.path(".").readStream()})].toMap())
    ) // TODO
    BuildSystem.internalCallEsBuild(browserCode, mainJsFile, outputPath = file, minify = True, sourceMap = True)
}

importAndRun(
    fireflyPath: Path
    target: String
    packagePair: PackagePair
    mainFile: String
    arguments: List[String]
): Bool {
    let process = Js.await(Js.dynamicImport("process"))
    let cwd = process->cwd()
    let workingDirectory = if(cwd->indexOf(":") === 1) {"file:///" + cwd?} else {cwd?}
    let packagePath = packagePair.group + "/" + packagePair.name
    let main = Js.await(Js.dynamicImport(
        workingDirectory + "/.firefly/output/" + target + "/" + packagePath + "/" + mainFile + ".mjs"
    ))
    if(main->"$run$".typeof() != "undefined") {
        main->"$run$"(fireflyPath.absolutePath, arguments)
        True
    } else {
        False
    }
}

prepareFireflyDirectory(path: Path) {
    if(!path.slash(".firefly").slash("output").exists()) {
        if(!path.slash(".firefly").exists()) {
            path.slash(".firefly").createDirectory()
        }
        path.slash(".firefly").slash("output").createDirectory()
    }
}

detectFireflyPath(system: NodeSystem): Path {
    let url = Js.import("url")
    let suffix = "/output/js/ff/compiler/Main.mjs"
    let moduleUrl = Js->import->meta->url
    if(!Js->import->meta->url->endsWith(suffix)?) {
        Js.throw(Js->Error->("Expected module path to end with: " + suffix + ", but got: " + moduleUrl?))
    } else {
        Path(url->fileURLToPath(Js->URL->(moduleUrl->slice(0, -suffix.size())))?)
    }
}
