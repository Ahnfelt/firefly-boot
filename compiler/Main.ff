import Tokenizer
import Parser
import Syntax
import Resolver
import Compiler
import Unification
import Builder
import Dependencies
import JsEmitter
import Inference

data MainCommand {
    BootstrapCommand
    RunCommand(mainPath: String, argument: List[String])
    BrowserCommand(mainPath: String)
    BuildCommand(mainPath: String)
    CheckCommand(filePath: String)
}

data CommandLineError(problem: String)

main(system: NodeSystem): Unit {

    let fireflyPath = detectFireflyPath()

    function buildScript(
        mainFile: String
        mainPackagePair: PackagePair
        emitTarget: EmitTarget
        resolvedDependencies: ResolvedDependencies
    ) {
        let fixedPackagePaths = if(resolvedDependencies.packagePaths.contains(PackagePair("ff", "core"))) {
            resolvedDependencies.packagePaths
        } else {
            resolvedDependencies.packagePaths.add(PackagePair("ff", "core"), fireflyPath + "/core")
        }
        let compilerModulePath = if(emitTarget != EmitBrowser && emitTarget != EmitExecutable) {
            fireflyPath + "/output/js/ff/compiler/Builder.mjs"
        }
        let targetName = emitTarget.{
            | EmitBuild => "build"
            | EmitNode => "node"
            | EmitBrowser => "browser"
            | EmitExecutable => "executable"
        }
        Builder.build(
            system = system
            emitTarget = emitTarget
            mainPackage = mainPackagePair
            mainModule = mainFile
            resolvedDependencies = resolvedDependencies.ResolvedDependencies(packagePaths = fixedPackagePaths)
            compilerModulePath = compilerModulePath
            tempPath = ".firefly/temporary"
            jsOutputPath = ".firefly/output/" + targetName
            printMeasurements = False
        )
    }

    function runCommand(command: MainCommand) {
        | RunCommand(mainFile, arguments) =>
            let resolvedDependencies = Dependencies.process(system.files(), system.fetch(), mainFile + ".ff")
            let fixedDependencies = resolvedDependencies.ResolvedDependencies(
                packagePaths = resolvedDependencies.packagePaths.add(resolvedDependencies.mainPackagePair, ".") // TODO: This path is wrong
            )
            prepareFireflyDirectory(system.files())
            buildScript(mainFile, resolvedDependencies.mainPackagePair, EmitNode, fixedDependencies)
            importAndRun(system.files(), fireflyPath, "node", resolvedDependencies.mainPackagePair, mainFile, arguments)

        | BrowserCommand(mainFile) =>
            let resolvedDependencies = Dependencies.process(system.files(), system.fetch(), mainFile + ".ff")
            let fixedDependencies = resolvedDependencies.ResolvedDependencies(
                packagePaths = resolvedDependencies.packagePaths.add(resolvedDependencies.mainPackagePair, ".") // TODO: This path is wrong
            )
            prepareFireflyDirectory(system.files())
            buildScript(mainFile, resolvedDependencies.mainPackagePair, EmitBrowser, fixedDependencies)
            bundleForBrowser(system, resolvedDependencies.mainPackagePair, mainFile)

        | BuildCommand(mainFile) =>
            let resolvedDependencies = Dependencies.process(system.files(), system.fetch(), mainFile + ".ff")
            let fixedDependencies = resolvedDependencies.ResolvedDependencies(
                packagePaths = resolvedDependencies.packagePaths.add(resolvedDependencies.mainPackagePair, ".") // TODO: This path is wrong
            )
            prepareFireflyDirectory(system.files())
            buildScript(mainFile, resolvedDependencies.mainPackagePair, EmitBuild, fixedDependencies)
            buildScript(mainFile, resolvedDependencies.mainPackagePair, EmitExecutable, fixedDependencies)
            bundleForPkg(system, resolvedDependencies.mainPackagePair, mainFile)
            importAndRun(system.files(), fireflyPath, "build", resolvedDependencies.mainPackagePair, mainFile, [])
        | CheckCommand(filePath) => 
            Builder.check(system, fireflyPath, filePath)
        | BootstrapCommand =>
            Builder.build(
                system = system
                emitTarget = EmitNode
                mainPackage = PackagePair("ff", "compiler")
                mainModule = "Main"
                resolvedDependencies = ResolvedDependencies(
                    mainPackagePair = PackagePair("ff", "compiler")
                    packages = [].toMap() // Only used for includes currently
                    packagePaths = [
                        Pair(PackagePair("ff", "compiler"), "compiler")
                        Pair(PackagePair("ff", "core"), "core")
                    ].toMap()
                    singleFilePackages = [].toSet()
                )
                compilerModulePath = None
                tempPath = "output/temporary"
                jsOutputPath = "output/js"
                printMeasurements = True
            )
    }

    try {
        let command = parseCommandLine(system.arguments().toList())
        runCommand(command)
    } catch {| CommandLineError(message), error =>
        Log.debug(message)
    } catch {| CompileError(at, message), error =>
        Log.debug(message)
        Log.debug(" at " + at.file.replace("./", "") + ":" + at.line + ":" + at.column)
    } grab()
}

usageString = """
usage: firefly <main-file> [<main-arguments>] | <command> [<command-arguments>]

These are the commands:
   run <main-file> [<main-arguments>]    Run the main file with the provided arguments
   browser <main-file>                   Compile the main file for the browser
   build <main-file>                     Build the main file
   check <firefly-file>                  Check the firefly source file for errors
   bootstrap                             Bootstrap the compiler
"""

parseCommandLine(arguments: List[String]): MainCommand {
    | [mainFile, ...mainArguments] { mainFile.removeLast(".ff") | Some(mainName) } =>
        RunCommand(mainName, mainArguments)
    | ["run", ...runArguments] =>
        runArguments.{
            | [mainFile, ...mainArguments] { mainFile.removeLast(".ff") | Some(mainName) } =>
                RunCommand(mainName, mainArguments)
            | _ => throw(CommandLineError(
                "You must specify a Firefly file (.ff) as first argument to run."  + usageString
            ))
        }
    | ["browser", ...browserArguments] =>
        browserArguments.{
            | [mainFile] { mainFile.removeLast(".ff") | Some(mainName) } =>
                BrowserCommand(mainName)
            | [_, _, ..._] => throw(CommandLineError(
                "You must only specify a single argument to browser."  + usageString
            ))
            | _ => throw(CommandLineError(
                "You must specify a Firefly file (.ff) as the argument to browser."  + usageString
            ))
        }
    | ["build", ...buildArguments] =>
        buildArguments.{
            | [mainFile] { mainFile.removeLast(".ff") | Some(mainName) } =>
                BuildCommand(mainName)
            | [_, _, ..._] => throw(CommandLineError(
                "You must only specify a single argument to build."  + usageString
            ))
            | _ => throw(CommandLineError(
                "You must specify a Firefly file (.ff) as the argument to build."  + usageString
            ))
        }
    | ["check", ...checkArguments] =>
        checkArguments.{
            | [fileName] { fileName.removeLast(".ff") | Some(_) } =>
                CheckCommand(fileName)
            | [_, _, ..._] => throw(CommandLineError(
                "You must only specify a single argument to check."  + usageString
            ))
            | _ => throw(CommandLineError(
                "You must specify a Firefly file (.ff) as the argument to build."  + usageString
            ))
        }
    | ["bootstrap", _] =>
        throw(CommandLineError("bootstrap takes no arguments" + usageString))
    | ["bootstrap"] => BootstrapCommand
    | [] => throw(CommandLineError("You must specify a command or a main file to run." + usageString))
    | [s, ..._] => throw(CommandLineError("Unknown command '" + s + "'" + usageString))
}

bundleForPkg(system: NodeSystem, packagePair: PackagePair, mainFile: String) {
    let prefix = ".firefly/output/executable/"
    let mainJsFile = prefix + packagePair.groupName("/") + "/" + mainFile + ".mjs"
    let file = prefix + "Main.bundle.js"
    BuildSystem.internalNodeCallEsBuild(system, mainJsFile, outputPath = file, minify = False)
}

bundleForBrowser(system: NodeSystem, packagePair: PackagePair, mainFile: String) {
    let prefix = ".firefly/output/browser/"
    let mainJsFile = prefix + packagePair.groupName("/") + "/" + mainFile + ".mjs"
    let file = prefix + "Main.bundle.js"
    let browserCode = BrowserCode(
        packageGroup = packagePair.group
        packageName = packagePair.name
        mainFile = mainFile
        assetSystem = AssetSystem([Pair("/", {system.files().readStream("")})].toMap())
    ) // TODO
    BuildSystem.internalCallEsBuild(browserCode, mainJsFile, outputPath = file, minify = True, sourceMap = True)
}

importAndRun(
    fs: FileSystem
    fireflyPath: String
    target: String
    packagePair: PackagePair
    mainFile: String
    arguments: List[String]
): Unit
    target node async """
        const process = await import('process');
        const cwd = process.cwd();
        const workingDirectory = cwd.indexOf(':') == 1 ? 'file:///' + cwd : cwd;
        const packagePath = packagePair_.group_ + "/" + packagePair_.name_
        const main = await import(workingDirectory + "/.firefly/output/" + target_ + "/" + packagePath + "/" + mainFile_ + ".mjs");
        await main.$run$(fireflyPath_, ff_core_List.List_toArray(arguments_))
    """

prepareFireflyDirectory(fs: FileSystem) {
    if(!fs.exists(".firefly/output")) {
        if(!fs.exists(".firefly")) {
            fs.createDirectory(".firefly")
        }
        fs.createDirectory(".firefly/output")
    }
}

detectBuildMain(fs: FileSystem, packagePair: PackagePair, mainFile: String): Bool {
    let file = mainFile + ".ff"
    let code = fs.readText(file)
    let tokens = Tokenizer.tokenize(file, code)
    let parser = Parser.make(PackagePair("script", "script"), file, tokens, False)
    let module = parser.parseModuleWithPackageInfo().module
    module.functions.any {definition =>
        (definition.signature.name == "buildMain" || definition.signature.name == "main") &&
        definition.signature.parameters.{ // This is a bit wrong, since BrowserSystem may be aliased or qualified
            | [Parameter p] {p.valueType | TConstructor(_, "BuildSystem", _)} => True
            | _ => False
        }
    }
}

detectFireflyPath(): String
    target node sync """
        import * as url from 'url';
        const suffix = '/output/js/ff/compiler/Main.mjs';
        const moduleUrl = import.meta.url;
        if(!import.meta.url.endsWith(suffix)) {
            throw 'Expected module path to end with: ' + suffix + ", but got: " + moduleUrl;
        }
        return '' + new URL(moduleUrl.slice(0, -suffix.length))
        //return url.fileURLToPath(new URL(moduleUrl.slice(0, -suffix.length)));
    """
