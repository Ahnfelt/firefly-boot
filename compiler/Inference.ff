import Syntax
import Unification
import Environment

type Inference(
    unification: Unification
)

make(instances: List[DInstance]): Inference {
    Inference(
        unification = Unification.make(instances)
    )
}

fail[T](at: Location, message: String): T {
    panic(message + " " + at.show)
}

//myInt = 42
//myString: String = myInt
//myFunction(x: Int): String { | a, b => x }
//myInt = Location("foo", 1, 1).Location(line = 2)
//myFoo: String = 1.{x => x}

core(name: String): String {
    "ff:core/Core." + name
}

extend self: Inference {

    inferModule(coreModule: Module, module: Module, otherModules: List[Module]): Module {
        // First infer everything, then substitute everything (failing on type variables that can't be substituted)

        let environment = Environment.make(coreModule, module, otherModules)

        let lets = module.lets.map { self.inferLetDefinition(environment, _) }
        let functions = module.functions.map { self.inferFunctionDefinition(environment, _) }
        //let extends = module.extends.map { self.inferExtendDefinition(_) }
        //let traits = module.traits.map { self.inferTraitDefinition(_) }
        //let instances = module.instances.map { self.inferInstanceDefinition(_) }
        module.Module(
            //traits = traits,
            //instances = instances,
            //extends = extends,
            lets = lets,
            functions = functions,
        )
    }

    inferLetDefinition(environment: Environment, definition: DLet): DLet {
        let value = self.inferTerm(environment, definition.variableType, definition.value)
        definition.DLet(
            value = value
        )
    }

    inferFunctionDefinition(environment: Environment, definition: DFunction): DFunction {
        let parameters = definition.signature.parameters.map { p =>
            let scheme = Scheme(True, False, Signature(p.at, p.name, [], [], [], p.valueType))
            Pair(p.name, scheme)
        }
        let environment2 = environment.Environment(symbols = environment.symbols ++ parameters)
        let functionType = TConstructor(
            definition.at,
            "Function$" + parameters.size,
            [...parameters.map { _.second.signature.returnType }, definition.signature.returnType]
        )
        definition.DFunction(body = self.inferLambda(environment2, functionType, definition.body))
    }

    inferLambda(environment: Environment, expected: Type, lambda: Lambda): Lambda {
        lambda.Lambda(
            cases = lambda.cases.map { self.inferMatchCase(environment, expected, _) }
        )
    }

    inferMatchCase(environment: Environment, expected: Type, case: MatchCase): MatchCase {
        let parameterTypes = case.patterns.map { self.unification.freshTypeVariable(_.at) }
        let returnType = self.unification.freshTypeVariable(case.at)
        let functionType = TConstructor(
            case.at,
            "Function$" + case.patterns.size,
            [...parameterTypes, returnType]
        )
        self.unification.unify(case.at, expected, functionType)
        let newEnvironment = (parameterTypes.zip(case.patterns).foldLeft(environment)) {| environment1, Pair(t, c) =>
            let symbols = self.inferPattern(environment, t, c).map {| Pair(name, type) =>
                Pair(name, Scheme(True, False, Signature(c.at, name, [], [], [], type)))
            }
            Environment(environment1.symbols ++ symbols)
        }
        // TODO: Condition
        let body = self.inferTerm(newEnvironment, returnType, case.body)
        case.MatchCase(
            body = body
        )
    }

    inferPattern(environment: Environment, expected: Type, pattern: MatchPattern): Map[String, Type] {
        pattern.{
            | PVariable(at, None) =>
                Map()
            | PVariable(at, Some(name)) =>
                Map(Pair(name, expected))
            | PAlias(at, pattern, variable) =>
                self.inferPattern(environment, expected, pattern) + Pair(variable, expected)
            | PList(at, t, items) =>
                let listType = TConstructor(at, core("List"), [t])
                self.unification.unify(at, expected, listType)
                (items.map {
                    | Pair(item, False) => self.inferPattern(environment, t, item)
                    | Pair(item, True) => self.inferPattern(environment, listType, item)
                }.foldLeft(Map[String, Type]())) { _ ++ _ }
            | PVariantAs(at, name, None) =>
                let scheme = environment.symbols.get(name).else {
                    fail(at, "No such variant: " + name)
                }
                Map()
            | PVariantAs(at, name, Some(variable)) =>
                let scheme = environment.symbols.get(name).else {
                    fail(at, "No such variant: " + name)
                }
                let parameters = scheme.signature.parameters
                let recordType =
                    TConstructor(at, "Record$" + parameters.map { _.name }.join("$"), parameters.map { _.valueType })
                Map(Pair(variable, recordType))
            | PVariant(at, name, patterns) =>
                let scheme = environment.symbols.get(name).else {
                    fail(at, "No such variant: " + name)
                }
                let parameters = scheme.signature.parameters
                (patterns.zip(parameters).map {| Pair(pattern, parameter) =>
                    self.inferPattern(environment, parameter.valueType, pattern)
                }.foldLeft(Map[String, Type]())) { _ ++ _ }
        }
    }

    inferTerm(environment: Environment, expected: Type, term: Term): Term {
        function literal(coreTypeName: String): Term {
            self.unification.unify(term.at, expected, TConstructor(term.at, core(coreTypeName), []))
            term
        }
        term.{
            | EString _ => literal("String")
            | EChar _ => literal("Char")
            | EInt _ => literal("Int")
            | EFloat _ => literal("Float")
            | EVariable e =>
                environment.symbols.get(e.name).map { scheme =>
                    if(scheme.isVariable) {
                        self.unification.unify(e.at, expected, scheme.signature.returnType)
                        term
                    } else {
                        fail(e.at, "Functions need to be called: " + e.name)
                    }
                } else {
                    fail(e.at, "Symbol not in scope: " + e.name)
                }
            | EList(at, t, items) =>
                let listType = TConstructor(term.at, core("List"), [t])
                EList(at, t, items.map { | Pair(item, spread) =>
                    Pair(self.inferTerm(environment, if(spread) { listType } else { t }, item), spread)
                })
            | ESequential(at, before, after) =>
                ESequential(
                    at = at
                    before = self.inferTerm(environment, self.unification.freshTypeVariable(at), before)
                    after = self.inferTerm(environment, expected, after)
                )
            | ELet e =>
                let scheme = Scheme(True, False, Signature(e.at, e.name, [], [], [], e.valueType))
                let environment2 = environment.Environment(symbols = environment.symbols + Pair(e.name, scheme))
                e.ELet(
                    value = self.inferTerm(environment, e.valueType, e.value)
                    body = self.inferTerm(environment2, expected, e.body)
                )
            | EVariant e =>
                let signature = environment.symbols.get(e.name).else {
                    fail(e.at, "Symbol not in scope: " + e.name)
                }.signature
                // TODO: Constrain the type arguments
                let typeArguments = self.inferTypeArguments(e.at, e.name, signature.generics, e.typeArguments)
                let instantiation = typeArguments.toMap
                let returnType = self.unification.instantiate(instantiation, signature.returnType)
                self.unification.unify(e.at, expected, returnType)
                let arguments = e.arguments.map {
                    self.inferArguments(e.at, environment, instantiation, signature.parameters, _)
                }
                e.EVariant(
                    typeArguments = typeArguments.map { _.second }
                    arguments = arguments
                )
            | EVariantIs e =>
                let signature = environment.symbols.get(e.name).else {
                    fail(e.at, "Symbol not in scope: " + e.name)
                }.signature
                let typeArguments = self.inferTypeArguments(e.at, e.name, signature.generics, e.typeArguments)
                e.EVariantIs(
                    typeArguments = typeArguments.map { _.second }
                )
            | ECopy e =>
                let signature = environment.symbols.get(e.name).else {
                    fail(e.at, "Symbol not in scope: " + e.name)
                }.signature
                e.arguments.find { a => !signature.parameters.exists { _.name == a.name } }.each {
                    | Field(at, name, value) => fail(at, "Unknown parameter: " + name)
                }
                let arguments = signature.parameters.map { p =>
                    e.arguments.find { _.name == p.name }.map {
                        | Field(at, _, value) => Argument(at, Some(p.name), value)
                    }.else {
                        Argument(e.at, Some(p.name), EField(e.at, EVariable(e.at, "_c", [], []), p.name))
                    }
                }
                let body = EVariant(e.at, e.name, [], Some(arguments))
                let term = EPipe(e.at, e.record,
                    ELambda(e.at, Lambda(e.at, [MatchCase(e.at, [PVariable(e.at, Some("_c"))], None, body)]))
                )
                self.inferTerm(environment, expected, term)
            | EPipe e =>
                let valueType = self.unification.freshTypeVariable(e.at)
                let functionType = TConstructor(e.at, "Function$1", [valueType, expected])
                let value = self.inferTerm(environment, valueType, e.value)
                let function = self.inferTerm(environment, functionType, e.function)
                e.EPipe(
                    value = value
                    function = function
                )
            | ELambda(at, l) =>
                let lambda = self.inferLambda(environment, expected, l)
                ELambda(at, lambda)
            | _ => term
            /*
            | EField e =>
                let recordType = self.unification.freshTypeVariable(e.at)
                let record = self.inferTerm(environment, recordType, e.record)
                self.unification.substitute(recordType).{
                    | TConstructor(_, name, typeParameters) =>
                        // TODO: Check that field e.name exists on record
                        e.EField(
                            record = record
                        )
                    | TVariable(_, index) =>
                        fail(e.at, "No such field " + e.field + " on unknown type: $" + index)
                }
            | ECall(at, function, typeArguments, arguments) =>
                ECall(
                    at = at
                    function = self.resolveTerm(function)
                    typeArguments = typeArguments.map { self.resolveType(_) }
                    arguments = arguments.map { a => a.Argument(value = self.resolveTerm(a.value)) }
                )
            | ERecord(at, fields) =>
                ERecord(
                    at = at
                    fields = fields.map { f => f.Field(value = self.resolveTerm(f.value)) }
                )
            | EWildcard e =>
                if(e.index == 0) { fail(e.at, "Unbound wildcard") }
                e
            | EFunctions(at, functions, body) =>
                let functionMap = functions.map { _.signature.name }.map { name => Pair(name, name) }.toMap
                let self2 = self.Resolver(variables = self.variables ++ functionMap)
                EFunctions(
                    at = at,
                    functions = functions.map { self2.resolveFunction(_) },
                    body = self2.resolveTerm(body)
                )
            | EAssign(at, operator, variable, value) =>
                EAssign(
                    at = at
                    operator = operator
                    variable = self.variables.get(variable).else { fail(at, "No such variable: " + variable) }
                    value = self.resolveTerm(value)
                )
            | EAssignField(at, operator, record, field, value) =>
                EAssignField(
                    at = at
                    operator = operator
                    record = self.resolveTerm(record)
                    field = field
                    value = self.resolveTerm(value)
                )
            */
        }
    }

    inferTypeArguments(
        at: Location
        name: String
        generics: List[String],
        typeArguments: List[Type]
    ): List[Pair[String, Type]] {
        if(typeArguments.nonEmpty) {
            if(generics.size != typeArguments.size) {
                fail(at,
                    "Wrong number of type parameters for " + name +
                    ", expected " + generics.size +
                    ", got " + typeArguments.size
                )
            }
            generics.zip(typeArguments)
        } else {
            generics.map { name =>
                Pair(name, self.unification.freshTypeVariable(at))
            }
        }
    }

    inferArguments(
        at: Location
        environment: Environment
        instantiation: Map[String, Type]
        parameters: List[Parameter]
        arguments: List[Argument]
    ): List[Argument] {
        mutable remainingArguments = arguments
        let newArguments = parameters.map { p =>
            let t = self.unification.instantiate(instantiation, p.valueType)
            remainingArguments.{
                | [] =>
                    p.default.map { e =>
                        let e2 = self.inferTerm(environment, t, e)
                        Argument(at, Some(p.name), e2)
                    }.else {
                        fail(at, "Missing argument: " + p.name)
                    }
                | [Argument(at, None, e), ...remaining] =>
                    remainingArguments = remaining.toList
                    let e2 = self.inferTerm(environment, t, e)
                    Argument(at, Some(p.name), e2)
                | remaining =>
                    remainingArguments.find { _.name.contains(p.name) }.map { | Argument(at, _, e) =>
                        remainingArguments = remainingArguments.filter { !_.name.contains(p.name) }
                        let e2 = self.inferTerm(environment, t, e)
                        Argument(at, Some(p.name), e2)
                    }.else {
                        fail(at, "Missing argument: " + p.name)
                    }
            }
        }
        remainingArguments.headOption.each {
            | Argument(at, None, _) => fail(at, "Too many arguments")
            | Argument(at, Some(name), _) => fail(at, "Unknown argument: " + name)
        }
        newArguments
    }

}
